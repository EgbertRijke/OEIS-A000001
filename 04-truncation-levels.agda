{-# OPTIONS --without-K --exact-split #-}

module 04-truncation-levels where

open import 03-contractible-types public

is-prop :
  {i : Level} (A : UU i) â†’ UU i
is-prop A = (x y : A) â†’ is-contr (Id x y)

is-prop-map : {l1 l2 : Level} {A : UU l1} {B : UU l2} (f : A â†’ B) â†’ UU (l1 âŠ” l2)
is-prop-map f = (b : _) â†’ is-prop (fib f b)

is-subtype : {l1 l2 : Level} {A : UU l1} (B : A â†’ UU l2) â†’ UU (l1 âŠ” l2)
is-subtype B = (x : _) â†’ is-prop (B x)

UU-Prop :
  (l : Level) â†’ UU (lsuc l)
UU-Prop l = Î£ (UU l) is-prop

type-Prop :
  {l : Level} â†’ UU-Prop l â†’ UU l
type-Prop P = pr1 P

is-prop-type-Prop :
  {l : Level} (P : UU-Prop l) â†’ is-prop (type-Prop P)
is-prop-type-Prop P = pr2 P

all-elements-equal :
  {i : Level} (A : UU i) â†’ UU i
all-elements-equal A = (x y : A) â†’ Id x y

is-prop-all-elements-equal :
  {i : Level} {A : UU i} â†’ all-elements-equal A â†’ is-prop A
is-prop-all-elements-equal {i} {A} H x y =
  pair
    ( (inv (H x x)) âˆ™ (H x y))
    ( Î» { refl â†’ left-inv (H x x)})

is-proof-irrelevant :
  {l1 : Level} â†’ UU l1 â†’ UU l1
is-proof-irrelevant A = A â†’ is-contr A

is-subterminal :
  {l1 : Level} â†’ UU l1 â†’ UU l1
is-subterminal A = is-emb (terminal-map {A = A})

eq-is-prop' :
  {i : Level} {A : UU i} â†’ is-prop A â†’ all-elements-equal A
eq-is-prop' H x y = pr1 (H x y)

eq-is-prop :
  {i : Level} {A : UU i} â†’ is-prop A â†’ {x y : A} â†’ Id x y
eq-is-prop H {x} {y} = eq-is-prop' H x y

is-proof-irrelevant-all-elements-equal :
  {l1 : Level} {A : UU l1} â†’ all-elements-equal A â†’ is-proof-irrelevant A
is-proof-irrelevant-all-elements-equal H a = pair a (H a)

is-proof-irrelevant-is-prop :
  {i : Level} {A : UU i} â†’ is-prop A â†’ is-proof-irrelevant A
is-proof-irrelevant-is-prop =
  is-proof-irrelevant-all-elements-equal âˆ˜ eq-is-prop'

is-prop-is-proof-irrelevant :
  {i : Level} {A : UU i} â†’ is-proof-irrelevant A â†’ is-prop A
is-prop-is-proof-irrelevant H x y = is-prop-is-contr (H x) x y

eq-is-proof-irrelevant :
  {l1 : Level} {A : UU l1} â†’ is-proof-irrelevant A â†’ all-elements-equal A
eq-is-proof-irrelevant H =
  eq-is-prop' (is-prop-is-proof-irrelevant H)

is-equiv-is-prop : {l1 l2 : Level} {A : UU l1} {B : UU l2} â†’ is-prop A â†’
  is-prop B â†’ {f : A â†’ B} â†’ (B â†’ A) â†’ is-equiv f
is-equiv-is-prop is-prop-A is-prop-B {f} g =
  is-equiv-has-inverse
    ( g)
    ( Î» y â†’ center (is-prop-B (f (g y)) y))
    ( Î» x â†’ center (is-prop-A (g (f x)) x))

equiv-prop :
  {l1 l2 : Level} {A : UU l1} {B : UU l2} â†’ is-prop A â†’ is-prop B â†’
  (A â†’ B) â†’ (B â†’ A) â†’ A â‰ƒ B
equiv-prop is-prop-A is-prop-B f g =
  pair f (is-equiv-is-prop is-prop-A is-prop-B g)

is-set :
  {i : Level} â†’ UU i â†’ UU i
is-set A = (x y : A) â†’ is-prop (Id x y)

UU-Set :
  (i : Level) â†’ UU (lsuc i)
UU-Set i = Î£ (UU i) is-set

type-Set :
  {l : Level} â†’ UU-Set l â†’ UU l
type-Set X = pr1 X

is-set-type-Set :
  {l : Level} (X : UU-Set l) â†’ is-set (type-Set X)
is-set-type-Set X = pr2 X

Id-Prop :
  {l : Level} (X : UU-Set l) (x y : type-Set X) â†’ UU-Prop l
Id-Prop X x y = pair (Id x y) (is-set-type-Set X x y)

data ğ•‹ : UU lzero where
  neg-two-ğ•‹ : ğ•‹
  succ-ğ•‹ : ğ•‹ â†’ ğ•‹

neg-one-ğ•‹ : ğ•‹
neg-one-ğ•‹ = succ-ğ•‹ (neg-two-ğ•‹)

zero-ğ•‹ : ğ•‹
zero-ğ•‹ = succ-ğ•‹ (neg-one-ğ•‹)

is-trunc : {i : Level} (k : ğ•‹) â†’ UU i â†’ UU i
is-trunc neg-two-ğ•‹ A = is-contr A
is-trunc (succ-ğ•‹ k) A = (x y : A) â†’ is-trunc k (Id x y)

is-trunc-map :
  {i j : Level} (k : ğ•‹) {A : UU i} {B : UU j} â†’ (A â†’ B) â†’ UU (i âŠ” j)
is-trunc-map k f = (y : _) â†’ is-trunc k (fib f y)

trunc-map : {i j : Level} (k : ğ•‹) (A : UU i) (B : UU j) â†’ UU (i âŠ” j)
trunc-map k A B = Î£ (A â†’ B) (is-trunc-map k)

UU-Truncated-Type : ğ•‹ â†’ (l : Level) â†’ UU (lsuc l)
UU-Truncated-Type k l = Î£ (UU l) (is-trunc k)

type-Truncated-Type :
  {k : ğ•‹} {l : Level} â†’ UU-Truncated-Type k l â†’ UU l
type-Truncated-Type = pr1

is-trunc-type-Truncated-Type :
  {k : ğ•‹} {l : Level} (A : UU-Truncated-Type k l) â†’
  is-trunc k (type-Truncated-Type A)
is-trunc-type-Truncated-Type = pr2

is-trunc-is-equiv :
  {i j : Level} (k : ğ•‹) {A : UU i} (B : UU j) (f : A â†’ B) â†’ is-equiv f â†’
  is-trunc k B â†’ is-trunc k A
is-trunc-is-equiv neg-two-ğ•‹ B f is-equiv-f H =
  is-contr-is-equiv B f is-equiv-f H
is-trunc-is-equiv (succ-ğ•‹ k) B f is-equiv-f H x y =
  is-trunc-is-equiv k (Id (f x) (f y)) (ap f {x} {y})
    ( is-emb-is-equiv is-equiv-f x y) (H (f x) (f y))
                                                  
is-set-is-equiv :
  {i j : Level} {A : UU i} (B : UU j) (f : A â†’ B) â†’ is-equiv f â†’
  is-set B â†’ is-set A
is-set-is-equiv = is-trunc-is-equiv zero-ğ•‹

is-trunc-equiv :
  {i j : Level} (k : ğ•‹) {A : UU i} (B : UU  j) (e : A â‰ƒ B) â†’
  is-trunc k B â†’ is-trunc k A
is-trunc-equiv k B (pair f is-equiv-f) =
  is-trunc-is-equiv k B f is-equiv-f

is-set-equiv :
  {i j : Level} {A : UU i} (B : UU j) (e : A â‰ƒ B) â†’
  is-set B â†’ is-set A
is-set-equiv = is-trunc-equiv zero-ğ•‹

is-trunc-is-equiv' :
  {i j : Level} (k : ğ•‹) (A : UU i) {B : UU j} (f : A â†’ B) â†’
  is-equiv f â†’ is-trunc k A â†’ is-trunc k B
is-trunc-is-equiv' k A  f is-equiv-f is-trunc-A =
  is-trunc-is-equiv k A
    ( map-inv-is-equiv is-equiv-f)
    ( is-equiv-map-inv-is-equiv is-equiv-f)
    ( is-trunc-A)

is-set-is-equiv' :
  {i j : Level} (A : UU i) {B : UU j} (f : A â†’ B) â†’ is-equiv f â†’
  is-set A â†’ is-set B
is-set-is-equiv' = is-trunc-is-equiv' zero-ğ•‹

is-trunc-equiv' :
  {i j : Level} (k : ğ•‹) (A : UU i) {B : UU j} (e : A â‰ƒ B) â†’
  is-trunc k A â†’ is-trunc k B
is-trunc-equiv' k A (pair f is-equiv-f) =
  is-trunc-is-equiv' k A f is-equiv-f

is-set-equiv' :
  {i j : Level} (A : UU i) {B : UU j} (e : A â‰ƒ B) â†’
  is-set A â†’ is-set B
is-set-equiv' = is-trunc-equiv' zero-ğ•‹

is-trunc-Î£ : {l1 l2 : Level} (k : ğ•‹) {A : UU l1} {B : A â†’ UU l2} â†’
  is-trunc k A â†’ ((x : A) â†’ is-trunc k (B x)) â†’ is-trunc k (Î£ A B)
is-trunc-Î£ neg-two-ğ•‹ is-trunc-A is-trunc-B =
  is-contr-Î£ is-trunc-A is-trunc-B
is-trunc-Î£ (succ-ğ•‹ k) {B = B} is-trunc-A is-trunc-B s t =
  is-trunc-is-equiv k
    ( Î£ (Id (pr1 s) (pr1 t)) (Î» p â†’ Id (tr B p (pr2 s)) (pr2 t)))
    ( pair-eq-Î£)
    ( is-equiv-pair-eq-Î£ s t)
    ( is-trunc-Î£ k
      ( is-trunc-A (pr1 s) (pr1 t))
      ( Î» p â†’ is-trunc-B (pr1 t) (tr B p (pr2 s)) (pr2 t)))

is-trunc-prod :
  {l1 l2 : Level} (k : ğ•‹) {A : UU l1} {B : UU l2} â†’
  is-trunc k A â†’ is-trunc k B â†’ is-trunc k (A Ã— B)
is-trunc-prod k is-trunc-A is-trunc-B =
  is-trunc-Î£ k is-trunc-A (Î» x â†’ is-trunc-B)

is-trunc-prod' :
  {l1 l2 : Level} (k : ğ•‹) {A : UU l1} {B : UU l2} â†’
  (B â†’ is-trunc (succ-ğ•‹ k) A) â†’ (A â†’ is-trunc (succ-ğ•‹ k) B) â†’
  is-trunc (succ-ğ•‹ k) (A Ã— B)
is-trunc-prod' k f g (pair a b) (pair a' b') =
  is-trunc-equiv k
    ( Eq-prod (pair a b) (pair a' b'))
    ( equiv-pair-eq (pair a b) (pair a' b'))
    ( is-trunc-prod k (f b a a') (g a b b'))

is-trunc-left-factor-prod :
  {l1 l2 : Level} (k : ğ•‹) {A : UU l1} {B : UU l2} â†’
  is-trunc k (A Ã— B) â†’ B â†’ is-trunc k A
is-trunc-left-factor-prod neg-two-ğ•‹ {A} {B} H b =
  is-contr-left-factor-prod A B H
is-trunc-left-factor-prod (succ-ğ•‹ k) H b a a' =
  is-trunc-left-factor-prod k {A = Id a a'} {B = Id b b}
    ( is-trunc-equiv' k
      ( Id (pair a b) (pair a' b))
      ( equiv-pair-eq (pair a b) (pair a' b))
      ( H (pair a b) (pair a' b)))
    ( refl)

is-trunc-right-factor-prod :
  {l1 l2 : Level} (k : ğ•‹) {A : UU l1} {B : UU l2} â†’
  is-trunc k (A Ã— B) â†’ A â†’ is-trunc k B
is-trunc-right-factor-prod neg-two-ğ•‹ {A} {B} H a =
  is-contr-right-factor-prod A B H
is-trunc-right-factor-prod (succ-ğ•‹ k) {A} {B} H a b b' =
  is-trunc-right-factor-prod k {A = Id a a} {B = Id b b'}
    ( is-trunc-equiv' k
      ( Id (pair a b) (pair a b'))
      ( equiv-pair-eq (pair a b) (pair a b'))
      ( H (pair a b) (pair a b')))
    ( refl)

is-prop-prod :
  {l1 l2 : Level} {A : UU l1} {B : UU l2} â†’
  is-prop A â†’ is-prop B â†’ is-prop (A Ã— B)
is-prop-prod = is-trunc-prod neg-one-ğ•‹

prod-Prop : {l1 l2 : Level} â†’ UU-Prop l1 â†’ UU-Prop l2 â†’ UU-Prop (l1 âŠ” l2)
prod-Prop P Q =
  pair
    ( type-Prop P Ã— type-Prop Q)
    ( is-prop-prod (is-prop-type-Prop P) (is-prop-type-Prop Q))

is-set-prod :
  {l1 l2 : Level} {A : UU l1} {B : UU l2} â†’
  is-set A â†’ is-set B â†’ is-set (A Ã— B)
is-set-prod = is-trunc-prod zero-ğ•‹

is-prop-is-equiv :
  {l1 l2 : Level} {A : UU l1} (B : UU l2) (f : A â†’ B) (E : is-equiv f) â†’
  is-prop B â†’ is-prop A
is-prop-is-equiv B f E H x y =
  is-contr-is-equiv _ (ap f {x} {y}) (is-emb-is-equiv E x y) (H (f x) (f y))

is-prop-equiv :
  {l1 l2 : Level} {A : UU l1} (B : UU l2) (e : A â‰ƒ B) â†’ is-prop B â†’ is-prop A
is-prop-equiv B (pair f is-equiv-f) = is-prop-is-equiv B f is-equiv-f

is-prop-is-equiv' :
  {l1 l2 : Level} (A : UU l1) {B : UU l2} (f : A â†’ B) (E : is-equiv f) â†’
  is-prop A â†’ is-prop B
is-prop-is-equiv' A f E H =
  is-prop-is-equiv _ (map-inv-is-equiv E) (is-equiv-map-inv-is-equiv E) H

is-prop-equiv' :
  {l1 l2 : Level} (A : UU l1) {B : UU l2} (e : A â‰ƒ B) â†’ is-prop A â†’ is-prop B
is-prop-equiv' A (pair f is-equiv-f) = is-prop-is-equiv' A f is-equiv-f

equiv-double-structure :
  {l1 l2 l3 : Level} {A : UU l1} (B : A â†’ UU l2) (C : A â†’ UU l3) â†’
  Î£ (Î£ A B) (Î» t â†’ C (pr1 t)) â‰ƒ Î£ (Î£ A C) (Î» t â†’ B (pr1 t))
equiv-double-structure {A = A} B C =
  ( ( inv-assoc-Î£ A C (Î» t â†’ B (pr1 t))) âˆ˜e
    ( equiv-tot (Î» x â†’ commutative-prod))) âˆ˜e
  ( assoc-Î£ A B (Î» t â†’ C (pr1 t)))

map-equiv-double-structure :
  {l1 l2 l3 : Level} {A : UU l1} (B : A â†’ UU l2) (C : A â†’ UU l3) â†’
  Î£ (Î£ A B) (Î» t â†’ C (pr1 t)) â†’ Î£ (Î£ A C) (Î» t â†’ B (pr1 t))
map-equiv-double-structure B C = map-equiv (equiv-double-structure B C)

is-equiv-map-equiv-double-structure :
  {l1 l2 l3 : Level} {A : UU l1} (B : A â†’ UU l2) (C : A â†’ UU l3) â†’
  is-equiv (map-equiv-double-structure B C)
is-equiv-map-equiv-double-structure B C =
  is-equiv-map-equiv (equiv-double-structure B C)
  
is-contr-total-Eq-substructure :
  {l1 l2 l3 : Level} {A : UU l1} {B : A â†’ UU l2} {P : A â†’ UU l3} â†’
  is-contr (Î£ A B) â†’ (is-subtype P) â†’ (a : A) (b : B a) (p : P a) â†’
  is-contr (Î£ (Î£ A P) (Î» t â†’ B (pr1 t)))
is-contr-total-Eq-substructure {A = A} {B} {P}
  is-contr-AB is-subtype-P a b p =
  is-contr-equiv
    ( Î£ (Î£ A B) (Î» t â†’ P (pr1 t)))
    ( equiv-double-structure P B)
    ( is-contr-equiv
      ( P a)
      ( left-unit-law-Î£-is-contr
        ( is-contr-AB)
        ( pair a b))
      ( is-proof-irrelevant-is-prop (is-subtype-P a) p))

Eq-total-subtype :
  {l1 l2 : Level} {A : UU l1} {B : A â†’ UU l2} â†’ is-subtype B â†’
  (Î£ A B) â†’ (Î£ A B) â†’ UU l1
Eq-total-subtype is-subtype-B p p' = Id (pr1 p) (pr1 p') 

reflexive-Eq-total-subtype :
  {l1 l2 : Level} {A : UU l1} {B : A â†’ UU l2} (is-subtype-B : is-subtype B) â†’
  (p : Î£ A B) â†’ Eq-total-subtype is-subtype-B p p
reflexive-Eq-total-subtype is-subtype-B (pair x y) = refl

Eq-total-subtype-eq :
  {l1 l2 : Level} {A : UU l1} {B : A â†’ UU l2} (is-subtype-B : is-subtype B) â†’
  (p p' : Î£ A B) â†’ Id p p' â†’ Eq-total-subtype is-subtype-B p p'
Eq-total-subtype-eq is-subtype-B p .p refl =
  reflexive-Eq-total-subtype is-subtype-B p

is-contr-total-Eq-total-subtype :
  {l1 l2 : Level} {A : UU l1} {B : A â†’ UU l2} (is-subtype-B : is-subtype B) â†’
  (p : Î£ A B) â†’ is-contr (Î£ (Î£ A B) (Eq-total-subtype is-subtype-B p))
is-contr-total-Eq-total-subtype is-subtype-B (pair x y) =
  is-contr-total-Eq-substructure
    ( is-contr-total-path x)
    ( is-subtype-B)
    x refl y

is-equiv-Eq-total-subtype-eq :
  {l1 l2 : Level} {A : UU l1} {B : A â†’ UU l2} (is-subtype-B : is-subtype B) â†’
  (p p' : Î£ A B) â†’ is-equiv (Eq-total-subtype-eq is-subtype-B p p')
is-equiv-Eq-total-subtype-eq is-subtype-B p =
  fundamental-theorem-id p
    ( reflexive-Eq-total-subtype is-subtype-B p)
    ( is-contr-total-Eq-total-subtype is-subtype-B p)
    ( Eq-total-subtype-eq is-subtype-B p)

equiv-Eq-total-subtype-eq :
  {l1 l2 : Level} {A : UU l1} {B : A â†’ UU l2} (is-subtype-B : is-subtype B) â†’
  (p p' : Î£ A B) â†’ (Id p p') â‰ƒ (Eq-total-subtype is-subtype-B p p')
equiv-Eq-total-subtype-eq is-subtype-B p p' =
  pair
    ( Eq-total-subtype-eq is-subtype-B p p')
    ( is-equiv-Eq-total-subtype-eq is-subtype-B p p')

eq-subtype :
  {l1 l2 : Level} {A : UU l1} {B : A â†’ UU l2} (is-subtype-B : is-subtype B) â†’
  {p p' : Î£ A B} â†’ Eq-total-subtype is-subtype-B p p' â†’ Id p p'
eq-subtype is-subtype-B {p} {p'} =
  map-inv-is-equiv (is-equiv-Eq-total-subtype-eq is-subtype-B p p')

is-trunc-succ-is-trunc :
  (k : ğ•‹) {i : Level} {A : UU i} â†’
  is-trunc k A â†’ is-trunc (succ-ğ•‹ k) A
is-trunc-succ-is-trunc neg-two-ğ•‹ H =
  is-prop-is-contr H
is-trunc-succ-is-trunc (succ-ğ•‹ k) H x y =
  is-trunc-succ-is-trunc k (H x y)

is-set-is-prop :
  {l : Level} {P : UU l} â†’ is-prop P â†’ is-set P
is-set-is-prop = is-trunc-succ-is-trunc neg-one-ğ•‹

is-trunc-map-succ-is-trunc-map :
  {l1 l2 : Level} (k : ğ•‹) {A : UU l1} {B : UU l2}
  (f : A â†’ B) â†’ is-trunc-map k f â†’ is-trunc-map (succ-ğ•‹ k) f
is-trunc-map-succ-is-trunc-map k f is-trunc-f b =
  is-trunc-succ-is-trunc k (is-trunc-f b)

truncated-type-succ-Truncated-Type :
  (k : ğ•‹) {l : Level} â†’ UU-Truncated-Type k l â†’ UU-Truncated-Type (succ-ğ•‹ k) l
truncated-type-succ-Truncated-Type k A =
  pair
    ( type-Truncated-Type A)
    ( is-trunc-succ-is-trunc k (is-trunc-type-Truncated-Type A))

set-Prop :
  {l : Level} â†’ UU-Prop l â†’ UU-Set l
set-Prop P = truncated-type-succ-Truncated-Type neg-one-ğ•‹ P

is-trunc-is-contr :
  { l : Level} (k : ğ•‹) {A : UU l} â†’ is-contr A â†’ is-trunc k A
is-trunc-is-contr neg-two-ğ•‹ is-contr-A = is-contr-A
is-trunc-is-contr (succ-ğ•‹ k) is-contr-A =
  is-trunc-succ-is-trunc k (is-trunc-is-contr k is-contr-A)

is-set-is-contr :
  {l : Level} {A : UU l} â†’ is-contr A â†’ is-set A
is-set-is-contr = is-trunc-is-contr zero-ğ•‹

is-trunc-is-prop :
  { l : Level} (k : ğ•‹) {A : UU l} â†’ is-prop A â†’ is-trunc (succ-ğ•‹ k) A
is-trunc-is-prop k is-prop-A x y = is-trunc-is-contr k (is-prop-A x y)

is-trunc-succ-empty : (k : ğ•‹) â†’ is-trunc (succ-ğ•‹ k) empty
is-trunc-succ-empty k = ind-empty

is-trunc-is-empty :
  {l : Level} (k : ğ•‹) {A : UU l} â†’ is-empty A â†’ is-trunc (succ-ğ•‹ k) A
is-trunc-is-empty k f = is-trunc-is-prop k (Î» x â†’ ex-falso (f x))

is-trunc-coprod : {l1 l2 : Level} (k : ğ•‹) {A : UU l1} {B : UU l2} â†’
  is-trunc (succ-ğ•‹ (succ-ğ•‹ k)) A â†’ is-trunc (succ-ğ•‹ (succ-ğ•‹ k)) B â†’
  is-trunc (succ-ğ•‹ (succ-ğ•‹ k)) (coprod A B)
is-trunc-coprod k {A} {B} is-trunc-A is-trunc-B (inl x) (inl y) =
  is-trunc-equiv (succ-ğ•‹ k)
    ( Eq-coprod A B (inl x) (inl y))
    ( equiv-Eq-eq-coprod A B (inl x) (inl y))
    ( is-trunc-equiv' (succ-ğ•‹ k)
      ( Id x y)
      ( equiv-raise _ (Id x y))
      ( is-trunc-A x y))
is-trunc-coprod k {A} {B} is-trunc-A is-trunc-B (inl x) (inr y) =
  is-trunc-equiv (succ-ğ•‹ k)
    ( Eq-coprod A B (inl x) (inr y))
    ( equiv-Eq-eq-coprod A B (inl x) (inr y))
    ( is-trunc-equiv' (succ-ğ•‹ k)
      ( empty)
      ( equiv-raise _ empty)
      ( is-trunc-succ-empty k))
is-trunc-coprod k {A} {B} is-trunc-A is-trunc-B (inr x) (inl y) =
  is-trunc-equiv (succ-ğ•‹ k)
    ( Eq-coprod A B (inr x) (inl y))
    ( equiv-Eq-eq-coprod A B (inr x) (inl y))
    ( is-trunc-equiv' (succ-ğ•‹ k)
      ( empty)
      ( equiv-raise _ empty)
      ( is-trunc-succ-empty k))
is-trunc-coprod k {A} {B} is-trunc-A is-trunc-B (inr x) (inr y) =
  is-trunc-equiv (succ-ğ•‹ k)
    ( Eq-coprod A B (inr x) (inr y))
    ( equiv-Eq-eq-coprod A B (inr x) (inr y))
    ( is-trunc-equiv' (succ-ğ•‹ k)
      ( Id x y)
      ( equiv-raise _ (Id x y))
      ( is-trunc-B x y))

is-set-coprod : {l1 l2 : Level} {A : UU l1} {B : UU l2} â†’
  is-set A â†’ is-set B â†’ is-set (coprod A B)
is-set-coprod = is-trunc-coprod neg-two-ğ•‹

coprod-Set :
  {l1 l2 : Level} (A : UU-Set l1) (B : UU-Set l2) â†’ UU-Set (l1 âŠ” l2)
coprod-Set (pair A is-set-A) (pair B is-set-B) =
  pair (coprod A B) (is-set-coprod is-set-A is-set-B)

is-prop-unit : is-prop unit
is-prop-unit = is-prop-is-contr is-contr-unit

unit-Prop : UU-Prop lzero
unit-Prop = pair unit is-prop-unit

is-prop-empty : is-prop empty
is-prop-empty ()

empty-Prop : UU-Prop lzero
empty-Prop = pair empty is-prop-empty

is-set-unit : is-set unit
is-set-unit = is-trunc-succ-is-trunc neg-one-ğ•‹ is-prop-unit

unit-Set : UU-Set lzero
unit-Set = pair unit is-set-unit

is-set-empty : is-set empty
is-set-empty ()

empty-Set : UU-Set lzero
empty-Set = pair empty is-set-empty

is-set-Fin :
  (n : â„•) â†’ is-set (Fin n)
is-set-Fin zero-â„• = is-set-empty
is-set-Fin (succ-â„• n) =
  is-set-coprod (is-set-Fin n) is-set-unit

Fin-Set :
  (n : â„•) â†’ UU-Set lzero
Fin-Set n = pair (Fin n) (is-set-Fin n)

is-equiv-prop-in-id :
  {i j : Level} {A : UU i}
  (R : A â†’ A â†’ UU j)
  (p : (x y : A) â†’ is-prop (R x y))
  (Ï : (x : A) â†’ R x x)
  (i : (x y : A) â†’ R x y â†’ Id x y) â†’
  (x y : A) â†’ is-equiv (i x y)
is-equiv-prop-in-id R p Ï i x =
  fundamental-theorem-id-retr x (i x)
    ( Î» y â†’ pair
      ( ind-Id x (Î» z p â†’ R x z) (Ï x) y)
      ( Î» r â†’ eq-is-prop (p x y)))

is-set-prop-in-id :
  {i j : Level} {A : UU i} (R : A â†’ A â†’ UU j)
  (p : (x y : A) â†’ is-prop (R x y))
  (Ï : (x : A) â†’ R x x)
  (i : (x y : A) â†’ R x y â†’ Id x y) â†’
  is-set A
is-set-prop-in-id R p Ï i x y =
  is-prop-is-equiv'
    ( R x y)
    ( i x y)
    ( is-equiv-prop-in-id R p Ï i x y) (p x y)

Eq-has-decidable-equality' :
  {l : Level} {A : UU l} (x y : A) â†’ is-decidable (Id x y) â†’ UU lzero
Eq-has-decidable-equality' x y (inl p) = unit
Eq-has-decidable-equality' x y (inr f) = empty

Eq-has-decidable-equality :
  {l : Level} {A : UU l} (d : has-decidable-equality A) â†’ A â†’ A â†’ UU lzero
Eq-has-decidable-equality d x y = Eq-has-decidable-equality' x y (d x y)

is-prop-Eq-has-decidable-equality' :
  {l : Level} {A : UU l} (x y : A) (t : is-decidable (Id x y)) â†’
  is-prop (Eq-has-decidable-equality' x y t)
is-prop-Eq-has-decidable-equality' x y (inl p) = is-prop-unit
is-prop-Eq-has-decidable-equality' x y (inr f) = is-prop-empty

is-prop-Eq-has-decidable-equality :
  {l : Level} {A : UU l} (d : has-decidable-equality A)
  {x y : A} â†’ is-prop (Eq-has-decidable-equality d x y)
is-prop-Eq-has-decidable-equality d {x} {y} =
  is-prop-Eq-has-decidable-equality' x y (d x y)

reflexive-Eq-has-decidable-equality :
  {l : Level} {A : UU l} (d : has-decidable-equality A) (x : A) â†’
  Eq-has-decidable-equality d x x 
reflexive-Eq-has-decidable-equality d x with d x x
... | inl Î± = star
... | inr f = f refl

Eq-has-decidable-equality-eq :
  {l : Level} {A : UU l} (d : has-decidable-equality A) {x y : A} â†’
  Id x y â†’ Eq-has-decidable-equality d x y
Eq-has-decidable-equality-eq {l} {A} d {x} {.x} refl =
  reflexive-Eq-has-decidable-equality d x

eq-Eq-has-decidable-equality' :
  {l : Level} {A : UU l} (x y : A) (t : is-decidable (Id x y)) â†’
  Eq-has-decidable-equality' x y t â†’ Id x y
eq-Eq-has-decidable-equality' x y (inl p) t = p
eq-Eq-has-decidable-equality' x y (inr f) t = ex-falso t

eq-Eq-has-decidable-equality :
  {l : Level} {A : UU l} (d : has-decidable-equality A) {x y : A} â†’
  Eq-has-decidable-equality d x y â†’ Id x y
eq-Eq-has-decidable-equality d {x} {y} =
  eq-Eq-has-decidable-equality' x y (d x y)

is-set-has-decidable-equality :
  {l : Level} {A : UU l} â†’ has-decidable-equality A â†’ is-set A
is-set-has-decidable-equality d =
  is-set-prop-in-id
    ( Î» x y â†’ Eq-has-decidable-equality d x y)
    ( Î» x y â†’ is-prop-Eq-has-decidable-equality d)
    ( Î» x â†’ reflexive-Eq-has-decidable-equality d x)
    ( Î» x y â†’ eq-Eq-has-decidable-equality d)

is-prop-Î£ : {l1 l2 : Level} {A : UU l1} {B : A â†’ UU l2} â†’
  is-prop A â†’ is-subtype B â†’ is-prop (Î£ A B)
is-prop-Î£ = is-trunc-Î£ neg-one-ğ•‹
                       
Î£-Prop :
  {l1 l2 : Level} (P : UU-Prop l1) (Q : type-Prop P â†’ UU-Prop l2) â†’
  UU-Prop (l1 âŠ” l2)
Î£-Prop P Q =
  pair
    ( Î£ (type-Prop P) (Î» p â†’ type-Prop (Q p)))
    ( is-prop-Î£
      ( is-prop-type-Prop P)
      ( Î» p â†’ is-prop-type-Prop (Q p)))

is-set-Î£ :
  {l1 l2 : Level} {A : UU l1} {B : A â†’ UU l2} â†’
  is-set A â†’ ((x : A) â†’ is-set (B x)) â†’ is-set (Î£ A B)
is-set-Î£ = is-trunc-Î£ zero-ğ•‹

Î£-Set :
  {l1 l2 : Level} (A : UU-Set l1) (B : pr1 A â†’ UU-Set l2) â†’ UU-Set (l1 âŠ” l2)
Î£-Set A B =
  pair
    ( Î£ (type-Set A) (Î» x â†’ (type-Set (B x))))
    ( is-set-Î£ (is-set-type-Set A) (Î» x â†’ is-set-type-Set (B x)))

prod-Set :
  {l1 l2 : Level} (A : UU-Set l1) (B : UU-Set l2) â†’ UU-Set (l1 âŠ” l2)
prod-Set A B = Î£-Set A (Î» x â†’ B)

is-trunc-is-emb : {i j : Level} (k : ğ•‹) {A : UU i} {B : UU j}
  (f : A â†’ B) â†’ is-emb f â†’ is-trunc (succ-ğ•‹ k) B â†’ is-trunc (succ-ğ•‹ k) A
is-trunc-is-emb k f Ef H x y =
  is-trunc-is-equiv k (Id (f x) (f y)) (ap f {x} {y}) (Ef x y) (H (f x) (f y))

is-trunc-emb : {i j : Level} (k : ğ•‹) {A : UU i} {B : UU j} â†’
  (f : A â†ª B) â†’ is-trunc (succ-ğ•‹ k) B â†’ is-trunc (succ-ğ•‹ k) A
is-trunc-emb k f = is-trunc-is-emb k (map-emb f) (is-emb-map-emb f)

total-subtype :
  {l1 l2 : Level} {A : UU l1} (P : A â†’ UU-Prop l2) â†’ UU (l1 âŠ” l2)
total-subtype {A = A} P = Î£ A (Î» x â†’ pr1 (P x))

is-prop-raise-unit :
  {l1 : Level} â†’ is-prop (raise-unit l1)
is-prop-raise-unit {l1} =
  is-prop-equiv' unit (equiv-raise l1 unit) is-prop-unit

is-emb-is-prop-map :
  {l1 l2 : Level} {A : UU l1} {B : UU l2} {f : A â†’ B} â†’
  is-prop-map f â†’ is-emb f
is-emb-is-prop-map {f = f} is-prop-map-f x =
  fundamental-theorem-id x refl
    ( is-contr-equiv
      ( fib f (f x))
      ( equiv-tot (Î» y â†’ equiv-inv (f x) (f y)))
      ( is-proof-irrelevant-is-prop (is-prop-map-f (f x)) (pair x refl)))
    ( Î» y â†’ ap f)

is-prop-map-is-emb :
  {l1 l2 : Level} {A : UU l1} {B : UU l2} {f : A â†’ B} â†’
  is-emb f â†’ is-prop-map f
is-prop-map-is-emb {f = f} is-emb-f y =
  is-prop-is-proof-irrelevant Î±
  where
  Î± : (t : fib f y) â†’ is-contr (fib f y)
  Î± (pair x refl) =
    fundamental-theorem-id' x refl
      ( Î» y â†’ inv âˆ˜ ap f)
      ( Î» y â†’
        is-equiv-comp' inv (ap f)
          ( is-emb-f x y)
          ( is-equiv-inv (f x) (f y)))

is-prop-map-emb :
  {l1 l2 : Level} {A : UU l1} {B : UU l2} (f : B â†ª A) â†’ is-prop-map (map-emb f)
is-prop-map-emb f = is-prop-map-is-emb (is-emb-map-emb f)

is-emb-is-injective' : {l1 l2 : Level} {A : UU l1} (is-set-A : is-set A)
  {B : UU l2} (is-set-B : is-set B) (f : A â†’ B) â†’
  is-injective f â†’ is-emb f
is-emb-is-injective' is-set-A is-set-B f is-injective-f x y =
  is-equiv-is-prop
    ( is-set-A x y)
    ( is-set-B (f x) (f y))
    ( is-injective-f)

is-set-is-injective :
  {l1 l2 : Level} {A : UU l1} {B : UU l2} {f : A â†’ B} â†’
  is-set B â†’ is-injective f â†’ is-set A
is-set-is-injective {f = f} H I =
  is-set-prop-in-id
    ( Î» x y â†’ Id (f x) (f y))
    ( Î» x y â†’ H (f x) (f y))
    ( Î» x â†’ refl)
    ( Î» x y â†’ I)

is-emb-is-injective :
  {l1 l2 : Level} {A : UU l1} {B : UU l2} {f : A â†’ B} â†’
  is-set B â†’ is-injective f â†’ is-emb f
is-emb-is-injective {f = f} H I =
  is-emb-is-injective' (is-set-is-injective H I) H f I

is-prop-map-is-injective :
  {l1 l2 : Level} {A : UU l1} {B : UU l2} {f : A â†’ B} â†’
  is-set B â†’ is-injective f â†’ is-prop-map f
is-prop-map-is-injective {f = f} H I =
  is-prop-map-is-emb (is-emb-is-injective H I)

is-prop-Eq-â„• :
  (n m : â„•) â†’ is-prop (Eq-â„• n m)
is-prop-Eq-â„• zero-â„• zero-â„• = is-prop-unit
is-prop-Eq-â„• zero-â„• (succ-â„• m) = is-prop-empty
is-prop-Eq-â„• (succ-â„• n) zero-â„• = is-prop-empty
is-prop-Eq-â„• (succ-â„• n) (succ-â„• m) = is-prop-Eq-â„• n m

is-set-â„• : is-set â„•
is-set-â„• =
  is-set-prop-in-id
    Eq-â„•
    is-prop-Eq-â„•
    refl-Eq-â„•
    eq-Eq-â„•

â„•-Set : UU-Set lzero
â„•-Set = pair â„• is-set-â„•

is-set-â„¤ : is-set â„¤
is-set-â„¤ = is-set-coprod is-set-â„• (is-set-coprod is-set-unit is-set-â„•)

â„¤-Set : UU-Set lzero
â„¤-Set = pair â„¤ is-set-â„¤

fiber-inclusion :
  {l1 l2 : Level} {A : UU l1} (B : A â†’ UU l2) (x : A) â†’ B x â†’ Î£ A B
fiber-inclusion B x = pair x

fib-fiber-inclusion :
  {l1 l2 : Level} {A : UU l1} (B : A â†’ UU l2) (a : A) (t : Î£ A B) â†’
  fib (fiber-inclusion B a) t â‰ƒ Id a (pr1 t)
fib-fiber-inclusion B a t =
  ( ( right-unit-law-Î£-is-contr
      ( Î» p â†’ is-contr-map-is-equiv (is-equiv-tr B p) (pr2 t))) âˆ˜e
    ( equiv-left-swap-Î£)) âˆ˜e
  ( equiv-tot (Î» b â†’ equiv-pair-eq-Î£ (pair a b) t))

is-trunc-is-trunc-map-fiber-inclusion :
  {l1 l2 : Level} (k : ğ•‹) {A : UU l1} â†’
  ((B : A â†’ UU l2) (a : A) â†’ is-trunc-map k (fiber-inclusion B a)) â†’
  is-trunc (succ-ğ•‹ k) A
is-trunc-is-trunc-map-fiber-inclusion {l1} {l2} k {A} H x y =
  is-trunc-equiv' k
    ( fib (fiber-inclusion B x) (pair y raise-star))
    ( fib-fiber-inclusion B x (pair y raise-star))
    ( H B x (pair y raise-star))
  where
  B : A â†’ UU l2
  B a = raise-unit l2

is-trunc-map-fiber-inclusion-is-trunc :
  {l1 l2 : Level} (k : ğ•‹) {A : UU l1} (B : A â†’ UU l2) (a : A) â†’
  is-trunc (succ-ğ•‹ k) A â†’ is-trunc-map k (fiber-inclusion B a)
is-trunc-map-fiber-inclusion-is-trunc k B a H t =
  is-trunc-equiv k
    ( Id a (pr1 t))
    ( fib-fiber-inclusion B a t)
    ( H a (pr1 t))

is-emb-fiber-inclusion :
  {l1 l2 : Level} {A : UU l1} (B : A â†’ UU l2) â†’
  is-set A â†’ (x : A) â†’ is-emb (fiber-inclusion B x)
is-emb-fiber-inclusion B H x =
  is-emb-is-prop-map (is-trunc-map-fiber-inclusion-is-trunc neg-one-ğ•‹ B x H)

emb-fiber-inclusion :
  {l1 l2 : Level} {A : UU l1} (B : A â†’ UU l2) â†’ is-set A â†’ (x : A) â†’ B x â†ª Î£ A B
emb-fiber-inclusion B H x =
  pair (fiber-inclusion B x) (is-emb-fiber-inclusion B H x)
