{-# OPTIONS --without-K --exact-split #-}

module 05-function-extensionality where

open import 04-truncation-levels public

htpy-eq :
  {i j : Level} {A : UU i} {B : A â†’ UU j} {f g : (x : A) â†’ B x} â†’
  (Id f g) â†’ (f ~ g)
htpy-eq refl = refl-htpy

FUNEXT :
  {i j : Level} {A : UU i} {B : A â†’ UU j} â†’
  (f : (x : A) â†’ B x) â†’ UU (i âŠ” j)
FUNEXT f = is-fiberwise-equiv (Î» g â†’ htpy-eq {f = f} {g = g})

WEAK-FUNEXT :
  {i j : Level} (A : UU i) (B : A â†’ UU j) â†’ UU (i âŠ” j)
WEAK-FUNEXT A B =
  ((x : A) â†’ is-contr (B x)) â†’ is-contr ((x : A) â†’ B x)

WEAK-FUNEXT-FUNEXT :
  {l1 l2 : Level} â†’
  ((A : UU l1) (B : A â†’ UU l2) (f : (x : A) â†’ B x) â†’ FUNEXT f) â†’
  ((A : UU l1) (B : A â†’ UU l2) â†’ WEAK-FUNEXT A B)
pr1 (WEAK-FUNEXT-FUNEXT funext A B is-contr-B) x = center (is-contr-B x)
pr2 (WEAK-FUNEXT-FUNEXT funext A B is-contr-B) f =
  map-inv-is-equiv
    ( funext A B (Î» x â†’ center (is-contr-B x)) f)
    ( Î» x â†’ contraction (is-contr-B x) (f x))

postulate funext : {i j : Level} {A : UU i} {B : A â†’ UU j} (f : (x : A) â†’ B x) â†’ FUNEXT f

equiv-funext : {i j : Level} {A : UU i} {B : A â†’ UU j} {f g : (x : A) â†’ B x} â†’
  (Id f g) â‰ƒ (f ~ g)
pr1 (equiv-funext {f = f} {g}) = htpy-eq
pr2 (equiv-funext {f = f} {g}) = funext f g

eq-htpy :
  {i j : Level} {A : UU i} {B : A â†’ UU j} {f g : (x : A) â†’ B x} â†’
  (f ~ g) â†’ Id f g
eq-htpy = map-inv-is-equiv (funext _ _)
  
issec-eq-htpy :
  {i j : Level} {A : UU i} {B : A â†’ UU j} {f g : (x : A) â†’ B x} â†’
  ((htpy-eq {f = f} {g = g}) âˆ˜ (eq-htpy {f = f} {g = g})) ~ id
issec-eq-htpy = issec-map-inv-is-equiv (funext _ _)
  
isretr-eq-htpy :
  {i j : Level} {A : UU i} {B : A â†’ UU j} {f g : (x : A) â†’ B x} â†’
  ((eq-htpy {f = f} {g = g}) âˆ˜ (htpy-eq {f = f} {g = g})) ~ id
isretr-eq-htpy = isretr-map-inv-is-equiv (funext _ _)

is-equiv-eq-htpy :
  {i j : Level} {A : UU i} {B : A â†’ UU j}
  (f g : (x : A) â†’ B x) â†’ is-equiv (eq-htpy {f = f} {g = g})
is-equiv-eq-htpy f g = is-equiv-map-inv-is-equiv (funext _ _)

eq-htpy-refl-htpy :
  {i j : Level} {A : UU i} {B : A â†’ UU j}
  (f : (x : A) â†’ B x) â†’ Id (eq-htpy (refl-htpy {f = f})) refl
eq-htpy-refl-htpy f = isretr-eq-htpy refl

equiv-eq-htpy :
  {i j : Level} {A : UU i} {B : A â†’ UU j} {f g : (x : A) â†’ B x} â†’
  (f ~ g) â‰ƒ Id f g
pr1 (equiv-eq-htpy {f = f} {g}) = eq-htpy
pr2 (equiv-eq-htpy {f = f} {g}) = is-equiv-eq-htpy f g

ev-refl-htpy :
  {l1 l2 l3 : Level} {A : UU l1} {B : A â†’ UU l2}
  (f : (x : A) â†’ B x) (C : (g : (x : A) â†’ B x) â†’ (f ~ g) â†’ UU l3) â†’
  ((g : (x : A) â†’ B x) (H : f ~ g) â†’ C g H) â†’ C f refl-htpy
ev-refl-htpy f C Ï† = Ï† f refl-htpy

IND-HTPY :
  {l1 l2 l3 : Level} {A : UU l1} {B : A â†’ UU l2}
  (f : (x : A) â†’ B x) â†’ UU _
IND-HTPY {l1} {l2} {l3} {A} {B} f =
  (C : (g : (x : A) â†’ B x) â†’ (f ~ g) â†’ UU l3) â†’ sec (ev-refl-htpy f C)

is-contr-total-htpy-FUNEXT :
  {i j : Level} {A : UU i} {B : A â†’ UU j} (f : (x : A) â†’ B x) â†’
  FUNEXT f â†’ is-contr (Î£ ((x : A) â†’ B x) (Î» g â†’ f ~ g))
is-contr-total-htpy-FUNEXT f funext-f =
  fundamental-theorem-id' f refl-htpy (Î» g â†’ htpy-eq {g = g}) funext-f

IND-HTPY-FUNEXT :
  {l1 l2 l3 : Level} {A : UU l1} {B : A â†’ UU l2} (f : (x : A) â†’ B x) â†’
  FUNEXT f â†’ IND-HTPY {l3 = l3} f
IND-HTPY-FUNEXT {l3 = l3} {A = A} {B = B} f funext-f =
  Ind-identity-system l3 f
    ( refl-htpy)
    ( is-contr-total-htpy-FUNEXT f funext-f)

Ind-htpy :
  {l1 l2 l3 : Level} {A : UU l1} {B : A â†’ UU l2} (f : (x : A) â†’ B x) â†’
  IND-HTPY {l3 = l3} f
Ind-htpy f = IND-HTPY-FUNEXT f (funext f)
  
ind-htpy :
  {l1 l2 l3 : Level} {A : UU l1} {B : A â†’ UU l2}
  (f : (x : A) â†’ B x) (C : (g : (x : A) â†’ B x) â†’ (f ~ g) â†’ UU l3) â†’
  C f refl-htpy â†’ {g : (x : A) â†’ B x} (H : f ~ g) â†’ C g H
ind-htpy f C t {g} = pr1 (Ind-htpy f C) t g
  
comp-htpy :
  {l1 l2 l3 : Level} {A : UU l1} {B : A â†’ UU l2}
  (f : (x : A) â†’ B x) (C : (g : (x : A) â†’ B x) â†’ (f ~ g) â†’ UU l3) â†’
  (c : C f refl-htpy) â†’
  Id (ind-htpy f C c refl-htpy) c
comp-htpy f C = pr2 (Ind-htpy f C)

is-contr-Î  :
  {l1 l2 : Level} {A : UU l1} {B : A â†’ UU l2} â†’
  ((x : A) â†’ is-contr (B x)) â†’ is-contr ((x : A) â†’ B x)
is-contr-Î  {A = A} {B = B} = WEAK-FUNEXT-FUNEXT (Î» X Y â†’ funext) A B

is-trunc-Î  :
  {l1 l2 : Level} (k : ğ•‹) {A : UU l1} {B : A â†’ UU l2} â†’
  ((x : A) â†’ is-trunc k (B x)) â†’ is-trunc k ((x : A) â†’ B x)
is-trunc-Î  neg-two-ğ•‹ is-trunc-B = is-contr-Î  is-trunc-B
is-trunc-Î  (succ-ğ•‹ k) is-trunc-B f g =
  is-trunc-is-equiv k (f ~ g) htpy-eq
    ( funext f g)
    ( is-trunc-Î  k (Î» x â†’ is-trunc-B x (f x) (g x)))

is-prop-Î  :
  {l1 l2 : Level} {A : UU l1} {B : A â†’ UU l2} â†’
  is-subtype B â†’ is-prop ((x : A) â†’ B x)
is-prop-Î  = is-trunc-Î  neg-one-ğ•‹

type-Î -Prop :
  {l1 l2 : Level} (A : UU l1) (P : A â†’ UU-Prop l2) â†’ UU (l1 âŠ” l2)
type-Î -Prop A P = (x : A) â†’ type-Prop (P x)

is-prop-type-Î -Prop :
  {l1 l2 : Level} (A : UU l1) (P : A â†’ UU-Prop l2) â†’ is-prop (type-Î -Prop A P)
is-prop-type-Î -Prop A P = is-prop-Î  (Î» x â†’ is-prop-type-Prop (P x))

Î -Prop :
  {l1 l2 : Level} (A : UU l1) â†’
  (A â†’ UU-Prop l2) â†’ UU-Prop (l1 âŠ” l2)
pr1 (Î -Prop A P) = type-Î -Prop A P
pr2 (Î -Prop A P) = is-prop-type-Î -Prop A P

type-function-Prop :
  {l1 l2 : Level} â†’ UU l1 â†’ UU-Prop l2 â†’ UU (l1 âŠ” l2)
type-function-Prop A P = A â†’ type-Prop P

is-trunc-function-type :
  {l1 l2 : Level} (k : ğ•‹) {A : UU l1} {B : UU l2} â†’
  is-trunc k B â†’ is-trunc k (A â†’ B)
is-trunc-function-type k {A} {B} is-trunc-B =
  is-trunc-Î  k {B = Î» (x : A) â†’ B} (Î» x â†’ is-trunc-B)
                                          
is-prop-function-type :
  {l1 l2 : Level} {A : UU l1} {B : UU l2} â†’
  is-prop B â†’ is-prop (A â†’ B)
is-prop-function-type = is-trunc-function-type neg-one-ğ•‹

is-set-function-type :
  {l1 l2 : Level} {A : UU l1} {B : UU l2} â†’
  is-set B â†’ is-set (A â†’ B)
is-set-function-type = is-trunc-function-type zero-ğ•‹

is-prop-type-function-Prop :
  {l1 l2 : Level} (A : UU l1) (P : UU-Prop l2) â†’
  is-prop (type-function-Prop A P)
is-prop-type-function-Prop A P =
  is-prop-function-type (is-prop-type-Prop P)

function-Prop :
  {l1 l2 : Level} â†’ UU l1 â†’ UU-Prop l2 â†’ UU-Prop (l1 âŠ” l2)
pr1 (function-Prop A P) = type-function-Prop A P
pr2 (function-Prop A P) = is-prop-type-function-Prop A P

type-hom-Prop :
  { l1 l2 : Level} (P : UU-Prop l1) (Q : UU-Prop l2) â†’ UU (l1 âŠ” l2)
type-hom-Prop P Q = type-function-Prop (type-Prop P) Q

is-prop-type-hom-Prop :
  {l1 l2 : Level} (P : UU-Prop l1) (Q : UU-Prop l2) â†’
  is-prop (type-hom-Prop P Q)
is-prop-type-hom-Prop P Q = is-prop-type-function-Prop (type-Prop P) Q

hom-Prop :
  { l1 l2 : Level} â†’ UU-Prop l1 â†’ UU-Prop l2 â†’ UU-Prop (l1 âŠ” l2)
pr1 (hom-Prop P Q) = type-hom-Prop P Q
pr2 (hom-Prop P Q) = is-prop-type-hom-Prop P Q

is-prop-Î ' :
  {l1 l2 : Level} {A : UU l1} {B : A â†’ UU l2} â†’
  is-subtype B â†’ is-prop ({x : A} â†’ B x)
is-prop-Î ' {l1} {l2} {A} {B} H =
  is-prop-equiv
    ( (x : A) â†’ B x)
    ( pair
      ( Î» f x â†’ f {x})
      ( is-equiv-has-inverse
        ( Î» g {x} â†’ g x)
        ( refl-htpy)
        ( refl-htpy)))
    ( is-prop-Î  H)

is-set-Î  :
  {l1 l2 : Level} {A : UU l1} {B : A â†’ UU l2} â†’
  ((x : A) â†’ is-set (B x)) â†’ is-set ((x : A) â†’ (B x))
is-set-Î  = is-trunc-Î  zero-ğ•‹

is-contr-total-htpy :
  {i j : Level} {A : UU i} {B : A â†’ UU j} (f : (x : A) â†’ B x) â†’
  is-contr (Î£ ((x : A) â†’ B x) (Î» g â†’ f ~ g))
pr1 (pr1 (is-contr-total-htpy f)) = f
pr2 (pr1 (is-contr-total-htpy f)) = refl-htpy
pr2 (is-contr-total-htpy f) t =
  ( inv
    ( contraction
      ( is-contr-total-htpy-FUNEXT f (funext f))
      ( pair f refl-htpy))) âˆ™
  ( contraction (is-contr-total-htpy-FUNEXT f (funext f)) t)

Î -total-fam :
  {l1 l2 l3 : Level} {A : UU l1} {B : A â†’ UU l2}
  (C : (x : A) â†’ B x â†’ UU l3) â†’ UU (l1 âŠ” (l2 âŠ” l3))
Î -total-fam {A = A} {B} C = (x : A) â†’ Î£ (B x) (C x)

type-choice-âˆ :
  {l1 l2 l3 : Level} {A : UU l1} {B : A â†’ UU l2}
  (C : (x : A) â†’ B x â†’ UU l3) â†’ UU (l1 âŠ” (l2 âŠ” l3))
type-choice-âˆ {A = A} {B} C = Î£ ((x : A) â†’ B x) (Î» f â†’ (x : A) â†’ C x (f x))

Eq-type-choice-âˆ :
  {l1 l2 l3 : Level} {A : UU l1} {B : A â†’ UU l2} (C : (x : A) â†’ B x â†’ UU l3)
  (t t' : type-choice-âˆ C) â†’ UU (l1 âŠ” (l2 âŠ” l3))
Eq-type-choice-âˆ {A = A} {B} C t t' =
  type-choice-âˆ
    ( Î» (x : A) (p : Id ((pr1 t) x) ((pr1 t') x)) â†’
      Id (tr (C x) p ((pr2 t) x)) ((pr2 t') x))

reflexive-Eq-type-choice-âˆ :
  {l1 l2 l3 : Level} {A : UU l1} {B : A â†’ UU l2} (C : (x : A) â†’ B x â†’ UU l3)
  (t : type-choice-âˆ C) â†’ Eq-type-choice-âˆ C t t
pr1 (reflexive-Eq-type-choice-âˆ C (pair f g)) = refl-htpy
pr2 (reflexive-Eq-type-choice-âˆ C (pair f g)) = refl-htpy

Eq-type-choice-âˆ-eq :
  {l1 l2 l3 : Level} {A : UU l1} {B : A â†’ UU l2} (C : (x : A) â†’ B x â†’ UU l3)
  (t t' : type-choice-âˆ C) â†’ Id t t' â†’ Eq-type-choice-âˆ C t t'
Eq-type-choice-âˆ-eq C t .t refl = reflexive-Eq-type-choice-âˆ C t

is-contr-total-Eq-type-choice-âˆ :
  {l1 l2 l3 : Level} {A : UU l1} {B : A â†’ UU l2} (C : (x : A) â†’ B x â†’ UU l3)
  (t : type-choice-âˆ C) â†’
  is-contr (Î£ (type-choice-âˆ C) (Eq-type-choice-âˆ C t))
is-contr-total-Eq-type-choice-âˆ {A = A} {B} C t =
  is-contr-total-Eq-structure
    ( Î» f g H â†’ (x : A) â†’ Id (tr (C x) (H x) ((pr2 t) x)) (g x))
    ( is-contr-total-htpy (pr1 t))
    ( pair (pr1 t) refl-htpy)
    ( is-contr-total-htpy (pr2 t))
  
is-equiv-Eq-type-choice-âˆ-eq :
  {l1 l2 l3 : Level} {A : UU l1} {B : A â†’ UU l2} (C : (x : A) â†’ B x â†’ UU l3)
  (t t' : type-choice-âˆ C) â†’ is-equiv (Eq-type-choice-âˆ-eq C t t')
is-equiv-Eq-type-choice-âˆ-eq C t =
  fundamental-theorem-id t
    ( reflexive-Eq-type-choice-âˆ C t)
    ( is-contr-total-Eq-type-choice-âˆ C t)
    ( Eq-type-choice-âˆ-eq C t)

eq-Eq-type-choice-âˆ :
  {l1 l2 l3 : Level} {A : UU l1} {B : A â†’ UU l2} (C : (x : A) â†’ B x â†’ UU l3)
  {t t' : type-choice-âˆ C} â†’ Eq-type-choice-âˆ C t t' â†’ Id t t'
eq-Eq-type-choice-âˆ C {t} {t'} =
  map-inv-is-equiv (is-equiv-Eq-type-choice-âˆ-eq C t t')

choice-âˆ :
  {l1 l2 l3 : Level} {A : UU l1} {B : A â†’ UU l2} {C : (x : A) â†’ B x â†’ UU l3} â†’
  Î -total-fam C â†’ type-choice-âˆ C
pr1 (choice-âˆ Ï†) x = pr1 (Ï† x)
pr2 (choice-âˆ Ï†) x = pr2 (Ï† x)

inv-choice-âˆ :
  {l1 l2 l3 : Level} {A : UU l1} {B : A â†’ UU l2} {C : (x : A) â†’ B x â†’ UU l3} â†’
  type-choice-âˆ C â†’ Î -total-fam C
pr1 (inv-choice-âˆ Ïˆ x) = pr1 Ïˆ x
pr2 (inv-choice-âˆ Ïˆ x) = pr2 Ïˆ x

issec-inv-choice-âˆ :
  {l1 l2 l3 : Level} {A : UU l1} {B : A â†’ UU l2} {C : (x : A) â†’ B x â†’ UU l3} â†’
  ( ( choice-âˆ {A = A} {B = B} {C = C}) âˆ˜
    ( inv-choice-âˆ {A = A} {B = B} {C = C})) ~ id
issec-inv-choice-âˆ {A = A} {C = C} (pair Ïˆ Ïˆ') =
  eq-Eq-type-choice-âˆ C (pair refl-htpy refl-htpy)

isretr-inv-choice-âˆ :
  {l1 l2 l3 : Level} {A : UU l1} {B : A â†’ UU l2} {C : (x : A) â†’ B x â†’ UU l3} â†’
  ( ( inv-choice-âˆ {A = A} {B = B} {C = C}) âˆ˜
    ( choice-âˆ {A = A} {B = B} {C = C})) ~ id
isretr-inv-choice-âˆ Ï† =
  eq-htpy (Î» x â†’ eq-pair-Î£ refl refl)

is-equiv-choice-âˆ :
  {l1 l2 l3 : Level} {A : UU l1} {B : A â†’ UU l2} {C : (x : A) â†’ B x â†’ UU l3} â†’
  is-equiv (choice-âˆ {A = A} {B = B} {C = C})
is-equiv-choice-âˆ {A = A} {B = B} {C = C} =
  is-equiv-has-inverse
    ( inv-choice-âˆ {A = A} {B = B} {C = C})
    ( issec-inv-choice-âˆ {A = A} {B = B} {C = C})
    ( isretr-inv-choice-âˆ {A = A} {B = B} {C = C})

equiv-choice-âˆ :
  { l1 l2 l3 : Level} {A : UU l1} {B : A â†’ UU l2} {C : (x : A) â†’ B x â†’ UU l3} â†’
  Î -total-fam C â‰ƒ type-choice-âˆ C
pr1 equiv-choice-âˆ = choice-âˆ
pr2 equiv-choice-âˆ = is-equiv-choice-âˆ

distributive-Î -Î£ :
  {l1 l2 l3 : Level} {A : UU l1} {B : A â†’ UU l2} {C : (x : A) â†’ B x â†’ UU l3} â†’
  Î -total-fam C â‰ƒ type-choice-âˆ C
distributive-Î -Î£ = equiv-choice-âˆ

is-equiv-inv-choice-âˆ :
  {l1 l2 l3 : Level} {A : UU l1} {B : A â†’ UU l2} {C : (x : A) â†’ B x â†’ UU l3} â†’
  is-equiv (inv-choice-âˆ {A = A} {B = B} {C = C})
is-equiv-inv-choice-âˆ {A = A} {B = B} {C = C} =
  is-equiv-has-inverse
    ( choice-âˆ {A = A} {B = B} {C = C})
    ( isretr-inv-choice-âˆ {A = A} {B = B} {C = C})
    ( issec-inv-choice-âˆ {A = A} {B = B} {C = C})

equiv-inv-choice-âˆ :
  {l1 l2 l3 : Level} {A : UU l1} {B : A â†’ UU l2} (C : (x : A) â†’ B x â†’ UU l3) â†’
  (type-choice-âˆ C) â‰ƒ (Î -total-fam C)
pr1 (equiv-inv-choice-âˆ C) = inv-choice-âˆ
pr2 (equiv-inv-choice-âˆ C) = is-equiv-inv-choice-âˆ

inv-distributive-Î -Î£ :
  {l1 l2 l3 : Level} {A : UU l1} {B : A â†’ UU l2} (C : (x : A) â†’ B x â†’ UU l3) â†’
  (type-choice-âˆ C) â‰ƒ (Î -total-fam C)
inv-distributive-Î -Î£ = equiv-inv-choice-âˆ

is-equiv-ev-pair :
  {l1 l2 l3 : Level} {A : UU l1} {B : A â†’ UU l2} {C : Î£ A B â†’ UU l3} â†’
  is-equiv (ev-pair {C = C})
pr1 (pr1 is-equiv-ev-pair) = ind-Î£
pr2 (pr1 is-equiv-ev-pair) = refl-htpy
pr1 (pr2 is-equiv-ev-pair) = ind-Î£
pr2 (pr2 is-equiv-ev-pair) f = eq-htpy (ind-Î£ (Î» x y â†’ refl))

equiv-ev-pair :
  {l1 l2 l3 : Level} {A : UU l1} {B : A â†’ UU l2} {C : Î£ A B â†’ UU l3} â†’
  ((x : Î£ A B) â†’ C x) â‰ƒ ((a : A) (b : B a) â†’ C (pair a b))
pr1 equiv-ev-pair = ev-pair
pr2 equiv-ev-pair = is-equiv-ev-pair

precomp-Î  :
  {l1 l2 l3 : Level} {A : UU l1} {B : UU l2} (f : A â†’ B) (C : B â†’ UU l3) â†’
  ((b : B) â†’ C b) â†’ ((a : A) â†’ C (f a))
precomp-Î  f C h a = h (f a)

tr-precompose-fam :
  {l1 l2 l3 : Level} {A : UU l1} {B : UU l2} (C : B â†’ UU l3)
  (f : A â†’ B) {x y : A} (p : Id x y) â†’ tr C (ap f p) ~ tr (Î» x â†’ C (f x)) p
tr-precompose-fam C f refl = refl-htpy

is-equiv-precomp-Î -is-coherently-invertible :
  {l1 l2 l3 : Level} {A : UU l1} {B : UU l2} (f : A â†’ B) â†’
  is-coherently-invertible f â†’
  (C : B â†’ UU l3) â†’ is-equiv (precomp-Î  f C)
is-equiv-precomp-Î -is-coherently-invertible f
  ( pair g (pair issec-g (pair isretr-g coh))) C = 
  is-equiv-has-inverse
    (Î» s y â†’ tr C (issec-g y) (s (g y)))
    ( Î» s â†’ eq-htpy (Î» x â†’ 
      ( ap (Î» t â†’ tr C t (s (g (f x)))) (coh x)) âˆ™
      ( ( tr-precompose-fam C f (isretr-g x) (s (g (f x)))) âˆ™
        ( apd s (isretr-g x)))))
    ( Î» s â†’ eq-htpy Î» y â†’ apd s (issec-g y))

is-equiv-precomp-Î -is-equiv :
  {l1 l2 l3 : Level} {A : UU l1} {B : UU l2} (f : A â†’ B) â†’ is-equiv f â†’
  (C : B â†’ UU l3) â†’ is-equiv (precomp-Î  f C)
is-equiv-precomp-Î -is-equiv f is-equiv-f =
  is-equiv-precomp-Î -is-coherently-invertible f
    ( is-coherently-invertible-is-path-split f
      ( is-path-split-is-equiv f is-equiv-f))

equiv-precomp-Î  :
  {l1 l2 l3 : Level} {A : UU l1} {B : UU l2} (e : A â‰ƒ B) â†’
  (C : B â†’ UU l3) â†’ ((b : B) â†’ C b) â‰ƒ ((a : A) â†’ C (map-equiv e a))
pr1 (equiv-precomp-Î  e C) = precomp-Î  (map-equiv e) C
pr2 (equiv-precomp-Î  e C) =
  is-equiv-precomp-Î -is-equiv (map-equiv e) (is-equiv-map-equiv e) C

ind-is-equiv :
  {l1 l2 l3 : Level} {A : UU l1} {B : UU l2}
  (C : B â†’ UU l3) (f : A â†’ B) (is-equiv-f : is-equiv f) â†’
  ((x : A) â†’ C (f x)) â†’ ((y : B) â†’ C y)
ind-is-equiv C f is-equiv-f =
  map-inv-is-equiv (is-equiv-precomp-Î -is-equiv f is-equiv-f C)
  
comp-is-equiv :
  {l1 l2 l3 : Level} {A : UU l1} {B : UU l2} (C : B â†’ UU l3)
  (f : A â†’ B) (is-equiv-f : is-equiv f) (h : (x : A) â†’ C (f x)) â†’
  Id (Î» x â†’ (ind-is-equiv C f is-equiv-f h) (f x)) h
comp-is-equiv C f is-equiv-f h =
  issec-map-inv-is-equiv (is-equiv-precomp-Î -is-equiv f is-equiv-f C) h
  
htpy-comp-is-equiv :
  {l1 l2 l3 : Level} {A : UU l1} {B : UU l2}
  (C : B â†’ UU l3) (f : A â†’ B) (is-equiv-f : is-equiv f)
  (h : (x : A) â†’ C (f x)) â†’
  (Î» x â†’ (ind-is-equiv C f is-equiv-f h) (f x)) ~ h
htpy-comp-is-equiv C f is-equiv-f h = htpy-eq (comp-is-equiv C f is-equiv-f h)

precomp :
  {l1 l2 l3 : Level} {A : UU l1} {B : UU l2} (f : A â†’ B) (C : UU l3) â†’
  (B â†’ C) â†’ (A â†’ C)
precomp f C = precomp-Î  f (Î» b â†’ C)

is-equiv-precomp-is-equiv-precomp-Î  :
  {l1 l2 l3 : Level} {A : UU l1} {B : UU l2} (f : A â†’ B) â†’
  ((C : B â†’ UU l3) â†’ is-equiv (precomp-Î  f C)) â†’
  ((C : UU l3) â†’ is-equiv (precomp f C))
is-equiv-precomp-is-equiv-precomp-Î  f is-equiv-precomp-Î -f C =
  is-equiv-precomp-Î -f (Î» y â†’ C)

is-equiv-precomp-is-equiv :
  {l1 l2 l3 : Level} {A : UU l1} {B : UU l2} (f : A â†’ B) â†’ is-equiv f â†’
  (C : UU l3) â†’ is-equiv (precomp f C)
is-equiv-precomp-is-equiv f is-equiv-f =
  is-equiv-precomp-is-equiv-precomp-Î  f
    ( is-equiv-precomp-Î -is-equiv f is-equiv-f)

equiv-precomp :
  {l1 l2 l3 : Level} {A : UU l1} {B : UU l2} (e : A â‰ƒ B) (C : UU l3) â†’
  (B â†’ C) â‰ƒ (A â†’ C)
pr1 (equiv-precomp e C) = precomp (map-equiv e) C
pr2 (equiv-precomp e C) =
  is-equiv-precomp-is-equiv (map-equiv e) (is-equiv-map-equiv e) C

is-equiv-is-equiv-precomp-subuniverse :
  { l1 l2 : Level}
  ( Î± : Level â†’ Level) (P : (l : Level) â†’ UU l â†’ UU (Î± l))
  ( A : Î£ (UU l1) (P l1)) (B : Î£ (UU l2) (P l2)) (f : pr1 A â†’ pr1 B) â†’
  ( (l : Level) (C : Î£ (UU l) (P l)) â†’
    is-equiv (precomp f (pr1 C))) â†’
  is-equiv f
is-equiv-is-equiv-precomp-subuniverse Î± P A B f is-equiv-precomp-f =
  let retr-f = center (is-contr-map-is-equiv (is-equiv-precomp-f _ A) id) in
  is-equiv-has-inverse
    ( pr1 retr-f)
    ( htpy-eq
      ( ap ( pr1)
           ( eq-is-contr'
             ( is-contr-map-is-equiv (is-equiv-precomp-f _ B) f)
               ( pair
                 ( f âˆ˜ (pr1 retr-f))
                 ( ap (Î» (g : pr1 A â†’ pr1 A) â†’ f âˆ˜ g) (pr2 retr-f)))
               ( pair id refl))))
    ( htpy-eq (pr2 retr-f))

is-equiv-is-equiv-precomp :
  {l1 l2 : Level} {A : UU l1} {B : UU l2} (f : A â†’ B) â†’
  ((l : Level) (C : UU l) â†’ is-equiv (precomp f C)) â†’ is-equiv f
is-equiv-is-equiv-precomp {A = A} {B = B} f is-equiv-precomp-f =
  is-equiv-is-equiv-precomp-subuniverse
    ( const Level Level lzero)
    ( Î» l X â†’ unit)
    ( pair A star)
    ( pair B star)
    ( f)
    ( Î» l C â†’ is-equiv-precomp-f l (pr1 C))

is-equiv-is-equiv-precomp-Prop :
  {l1 l2 : Level} (P : UU-Prop l1) (Q : UU-Prop l2)
  (f : type-Prop P â†’ type-Prop Q) â†’
  ({l : Level} (R : UU-Prop l) â†’ is-equiv (precomp f (type-Prop R))) â†’
  is-equiv f
is-equiv-is-equiv-precomp-Prop P Q f H =
  is-equiv-is-equiv-precomp-subuniverse id (Î» l â†’ is-prop) P Q f (Î» l â†’ H {l})

is-equiv-is-equiv-precomp-Set :
  {l1 l2 : Level} (A : UU-Set l1) (B : UU-Set l2)
  (f : type-Set A â†’ type-Set B) â†’
  ({l : Level} (C : UU-Set l) â†’ is-equiv (precomp f (type-Set C))) â†’
  is-equiv f
is-equiv-is-equiv-precomp-Set A B f H =
  is-equiv-is-equiv-precomp-subuniverse id (Î» l â†’ is-set) A B f (Î» l â†’ H {l})

is-equiv-is-equiv-precomp-Truncated-Type :
  {l1 l2 : Level} (k : ğ•‹)
  (A : UU-Truncated-Type k l1) (B : UU-Truncated-Type k l2)
  (f : type-Truncated-Type A â†’ type-Truncated-Type B) â†’
  ({l : Level} (C : UU-Truncated-Type k l) â†’ is-equiv (precomp f (pr1 C))) â†’
  is-equiv f
is-equiv-is-equiv-precomp-Truncated-Type k A B f H =
    is-equiv-is-equiv-precomp-subuniverse id (Î» l â†’ is-trunc k) A B f
      ( Î» l â†’ H {l})

postcomp :
  {l1 l2 l3 : Level} {X : UU l1} {Y : UU l2} (A : UU l3) â†’
  (X â†’ Y) â†’ (A â†’ X) â†’ (A â†’ Y)
postcomp A f h = f âˆ˜ h

map-Î  :
  {l1 l2 l3 : Level} {I : UU l1} {A : I â†’ UU l2} {B : I â†’ UU l3}
  (f : (i : I) â†’ A i â†’ B i) â†’
  ((i : I) â†’ A i) â†’ ((i : I) â†’ B i)
map-Î  f h i = f i (h i)

htpy-map-Î  :
  {l1 l2 l3 : Level} {I : UU l1} {A : I â†’ UU l2} {B : I â†’ UU l3}
  {f f' : (i : I) â†’ A i â†’ B i} (H : (i : I) â†’ (f i) ~ (f' i)) â†’
  (map-Î  f) ~ (map-Î  f')
htpy-map-Î  H h = eq-htpy (Î» i â†’ H i (h i))

map-Î ' :
  {l1 l2 l3 l4 : Level} {I : UU l1} {A : I â†’ UU l2} {B : I â†’ UU l3}
  {J : UU l4} (Î± : J â†’ I) â†’ 
  ((i : I) â†’ A i â†’ B i) â†’ ((j : J) â†’ A (Î± j)) â†’ ((j : J) â†’ B (Î± j))
map-Î ' Î± f = map-Î  (Î» j â†’ f (Î± j))

htpy-map-Î ' :
  {l1 l2 l3 l4 : Level} {I : UU l1} {A : I â†’ UU l2} {B : I â†’ UU l3}
  {J : UU l4} (Î± : J â†’ I) {f f' : (i : I) â†’ A i â†’ B i} â†’
  ((i : I) â†’ (f i) ~ (f' i)) â†’ (map-Î ' Î± f ~ map-Î ' Î± f')
htpy-map-Î ' Î± H = htpy-map-Î  (Î» j â†’ H (Î± j))

equiv-fib-map-Î  :
  {l1 l2 l3 : Level} {I : UU l1} {A : I â†’ UU l2} {B : I â†’ UU l3}
  (f : (i : I) â†’ A i â†’ B i) (h : (i : I) â†’ B i) â†’
  ((i : I) â†’ fib (f i) (h i)) â‰ƒ fib (map-Î  f) h
equiv-fib-map-Î  f h =
  equiv-tot (Î» x â†’ equiv-eq-htpy) âˆ˜e equiv-choice-âˆ

is-trunc-map-map-Î  :
  (k : ğ•‹) {l1 l2 l3 : Level} {I : UU l1} {A : I â†’ UU l2} {B : I â†’ UU l3}
  (f : (i : I) â†’ A i â†’ B i) â†’
  ((i : I) â†’ is-trunc-map k (f i)) â†’ is-trunc-map k (map-Î  f)
is-trunc-map-map-Î  k {I = I} f H h =
  is-trunc-equiv' k
    ( (i : I) â†’ fib (f i) (h i))
    ( equiv-fib-map-Î  f h)
    ( is-trunc-Î  k (Î» i â†’ H i (h i)))

is-equiv-map-Î  :
  {l1 l2 l3 : Level} {I : UU l1} {A : I â†’ UU l2} {B : I â†’ UU l3}
  (f : (i : I) â†’ A i â†’ B i) (is-equiv-f : is-fiberwise-equiv f) â†’
  is-equiv (map-Î  f)
is-equiv-map-Î  f is-equiv-f =
  is-equiv-is-contr-map
    ( is-trunc-map-map-Î  neg-two-ğ•‹ f
      ( Î» i â†’ is-contr-map-is-equiv (is-equiv-f i)))

equiv-map-Î  :
  {l1 l2 l3 : Level} {I : UU l1} {A : I â†’ UU l2} {B : I â†’ UU l3}
  (e : (i : I) â†’ (A i) â‰ƒ (B i)) â†’ ((i : I) â†’ A i) â‰ƒ ((i : I) â†’ B i)
pr1 (equiv-map-Î  e) = map-Î  (Î» i â†’ map-equiv (e i))
pr2 (equiv-map-Î  e) = is-equiv-map-Î  _ (Î» i â†’ is-equiv-map-equiv (e i))

module _
  { l1 l2 l3 l4 : Level}
  { A' : UU l1} {B' : A' â†’ UU l2} {A : UU l3} (B : A â†’ UU l4)
  ( e : A' â‰ƒ A) (f : (a' : A') â†’ B' a' â‰ƒ B (map-equiv e a'))
  where
  
  map-equiv-Î  : ((a' : A') â†’ B' a') â†’ ((a : A) â†’ B a)
  map-equiv-Î  =
    ( map-Î 
      ( Î» a â†’
        ( tr B (issec-map-inv-equiv e a)) âˆ˜
        ( map-equiv (f (map-inv-equiv e a))))) âˆ˜
    ( precomp-Î  (map-inv-equiv e) B')

  compute-map-equiv-Î  :
    (h : (a' : A') â†’ B' a') (a' : A') â†’
    Id ( map-equiv-Î  h (map-equiv e a')) (map-equiv (f a') (h a'))
  compute-map-equiv-Î  h a' =
    ( ap
      ( Î» t â†’
        tr B t ( map-equiv
                 ( f (map-inv-equiv e (map-equiv e a')))
                 ( h (map-inv-equiv e (map-equiv e a')))))
      ( coherence-map-inv-equiv e a')) âˆ™
    ( ( tr-ap
        ( map-equiv e)
        ( Î» _ â†’ id)
        ( isretr-map-inv-equiv e a')
        ( map-equiv
          ( f (map-inv-equiv e (map-equiv e a')))
          ( h (map-inv-equiv e (map-equiv e a'))))) âˆ™
      ( Î± ( map-inv-equiv e (map-equiv e a'))
          ( isretr-map-inv-equiv e a')))
    where
    Î± : (x : A') (p : Id x a') â†’
        Id ( tr (B âˆ˜ map-equiv e) p (map-equiv (f x) (h x)))
           ( map-equiv (f a') (h a'))
    Î± x refl = refl

  is-equiv-map-equiv-Î  : is-equiv map-equiv-Î 
  is-equiv-map-equiv-Î  =
    is-equiv-comp'
      ( map-Î  (Î» a â†’
        ( tr B (issec-map-inv-is-equiv (is-equiv-map-equiv e) a)) âˆ˜
        ( map-equiv (f (map-inv-is-equiv (is-equiv-map-equiv e) a)))))
      ( precomp-Î  (map-inv-is-equiv (is-equiv-map-equiv e)) B')
      ( is-equiv-precomp-Î -is-equiv
        ( map-inv-is-equiv (is-equiv-map-equiv e))
        ( is-equiv-map-inv-is-equiv (is-equiv-map-equiv e))
        ( B'))
      ( is-equiv-map-Î  _
        ( Î» a â†’ is-equiv-comp'
          ( tr B (issec-map-inv-is-equiv (is-equiv-map-equiv e) a))
          ( map-equiv (f (map-inv-is-equiv (is-equiv-map-equiv e) a)))
          ( is-equiv-map-equiv
            ( f (map-inv-is-equiv (is-equiv-map-equiv e) a)))
          ( is-equiv-tr B (issec-map-inv-is-equiv (is-equiv-map-equiv e) a))))

  equiv-Î  : ((a' : A') â†’ B' a') â‰ƒ ((a : A) â†’ B a)
  pr1 equiv-Î  = map-equiv-Î 
  pr2 equiv-Î  = is-equiv-map-equiv-Î 

id-map-equiv-Î  :
  { l1 l2 : Level} {A : UU l1} (B : A â†’ UU l2) â†’
  ( map-equiv-Î  B (equiv-id {A = A}) (Î» a â†’ equiv-id {A = B a})) ~ id
id-map-equiv-Î  B = refl-htpy

module _
  {l1 l2 : Level} {A : UU l1} {B : UU l2}
  where

  ev-inl-inr :
    {l3 : Level} (P : coprod A B â†’ UU l3) â†’
    ((t : coprod A B) â†’ P t) â†’ ((x : A) â†’ P (inl x)) Ã— ((y : B) â†’ P (inr y))
  pr1 (ev-inl-inr P s) x = s (inl x)
  pr2 (ev-inl-inr P s) y = s (inr y)

  dependent-universal-property-coprod :
    {l3 : Level} (P : coprod A B â†’ UU l3) â†’ is-equiv (ev-inl-inr P)
  dependent-universal-property-coprod P =
    is-equiv-has-inverse
      ( Î» p â†’ ind-coprod P (pr1 p) (pr2 p))
      ( ind-Î£ (Î» f g â†’ eq-pair refl refl))
      ( Î» s â†’ eq-htpy (ind-coprod _ (Î» x â†’ refl) Î» y â†’ refl))

  equiv-dependent-universal-property-coprod :
    {l3 : Level} (P : coprod A B â†’ UU l3) â†’
    ((x : coprod A B) â†’ P x) â‰ƒ (((a : A) â†’ P (inl a)) Ã— ((b : B) â†’ P (inr b)))
  pr1 (equiv-dependent-universal-property-coprod P) = ev-inl-inr P
  pr2 (equiv-dependent-universal-property-coprod P) =
    dependent-universal-property-coprod P
  
  universal-property-coprod :
    {l3 : Level} (X : UU l3) â†’
    is-equiv (ev-inl-inr (Î» (t : coprod A B) â†’ X))
  universal-property-coprod X = dependent-universal-property-coprod (Î» t â†’ X)
  
  equiv-universal-property-coprod :
    {l3 : Level} (X : UU l3) â†’
    (coprod A B â†’ X) â‰ƒ ((A â†’ X) Ã— (B â†’ X))
  equiv-universal-property-coprod X =
    equiv-dependent-universal-property-coprod (Î» t â†’ X)
  
  uniqueness-coprod :
    {l3 : Level} {Y : UU l3} (i : A â†’ Y) (j : B â†’ Y) â†’
    ((l : Level) (X : UU l) â†’
      is-equiv (Î» (s : Y â†’ X) â†’ pair' (s âˆ˜ i) (s âˆ˜ j))) â†’
    is-equiv (ind-coprod (Î» t â†’ Y) i j)
  uniqueness-coprod {Y = Y} i j H =
    is-equiv-is-equiv-precomp
      ( ind-coprod _ i j)
      ( Î» l X â†’ is-equiv-right-factor'
        ( ev-inl-inr (Î» t â†’ X))
        ( precomp (ind-coprod (Î» t â†’ Y) i j) X)
        ( universal-property-coprod X)
        ( H _ X))

  universal-property-coprod-is-equiv-ind-coprod :
    {l3 : Level} (X : UU l3) (i : A â†’ X) (j : B â†’ X) â†’
    is-equiv (ind-coprod (Î» t â†’ X) i j) â†’
    (l4 : Level) (Y : UU l4) â†’
      is-equiv (Î» (s : X â†’ Y) â†’ pair' (s âˆ˜ i) (s âˆ˜ j))
  universal-property-coprod-is-equiv-ind-coprod X i j H l Y =
    is-equiv-comp
      ( Î» s â†’ pair (s âˆ˜ i) (s âˆ˜ j))
      ( ev-inl-inr (Î» t â†’ Y))
      ( precomp (ind-coprod (Î» t â†’ X) i j) Y)
      ( Î» s â†’ refl)
      ( is-equiv-precomp-is-equiv
        ( ind-coprod (Î» t â†’ X) i j)
        ( H)
        ( Y))
      ( universal-property-coprod Y)

fib-emb-Prop :
  {i j : Level} {A : UU i} {B : UU j} (f : A â†ª B) â†’ B â†’ UU-Prop (i âŠ” j)
pr1 (fib-emb-Prop f y) = fib (map-emb f) y
pr2 (fib-emb-Prop f y) = is-prop-map-is-emb (is-emb-map-emb f) y

is-emb-pr1 :
  {l1 l2 : Level} {A : UU l1} {B : A â†’ UU l2} â†’
  is-subtype B â†’ is-emb (pr1 {B = B})
is-emb-pr1 {B = B} H =
  is-emb-is-prop-map (Î» x â†’ is-prop-equiv (B x) (equiv-fib-pr1 x) (H x))

equiv-ap-pr1 : {i j : Level} {A : UU i} {B : A â†’ UU j} â†’
  is-subtype B â†’ {s t : Î£ A B} â†’ Id s t â‰ƒ Id (pr1 s) (pr1 t)
pr1 (equiv-ap-pr1 is-subtype-B {s} {t}) = ap pr1
pr2 (equiv-ap-pr1 is-subtype-B {s} {t}) = is-emb-pr1 is-subtype-B s t

is-subtype-is-emb-pr1 : {i j : Level} {A : UU i} {B : A â†’ UU j} â†’
  is-emb (pr1 {B = B}) â†’ is-subtype B
is-subtype-is-emb-pr1 H x =
  is-prop-equiv' (fib pr1 x) (equiv-fib-pr1 x) (is-prop-map-is-emb H x)

module _
  {l1 l2 : Level} {A : UU l1} {B : UU l2}
  where

  is-contr-sec-is-equiv : {f : A â†’ B} â†’ is-equiv f â†’ is-contr (sec f)
  is-contr-sec-is-equiv {f} is-equiv-f =
    is-contr-equiv'
      ( (b : B) â†’ fib f b)
      ( equiv-choice-âˆ) 
      ( is-contr-Î  (is-contr-map-is-equiv is-equiv-f))

  is-contr-retr-is-equiv : {f : A â†’ B} â†’ is-equiv f â†’ is-contr (retr f)
  is-contr-retr-is-equiv {f} is-equiv-f =
    is-contr-is-equiv'
      ( Î£ (B â†’ A) (Î» h â†’ Id (h âˆ˜ f) id))
      ( tot (Î» h â†’ htpy-eq))
      ( is-equiv-tot-is-fiberwise-equiv
        ( Î» h â†’ funext (h âˆ˜ f) id))
      ( is-contr-map-is-equiv (is-equiv-precomp-is-equiv f is-equiv-f A) id)

  is-contr-is-equiv-is-equiv : {f : A â†’ B} â†’ is-equiv f â†’ is-contr (is-equiv f)
  is-contr-is-equiv-is-equiv is-equiv-f =
    is-contr-prod
      ( is-contr-sec-is-equiv is-equiv-f)
      ( is-contr-retr-is-equiv is-equiv-f)

  is-subtype-is-equiv : is-subtype (is-equiv {A = A} {B = B})
  is-subtype-is-equiv f = is-prop-is-proof-irrelevant
    ( Î» is-equiv-f â†’ is-contr-prod
      ( is-contr-sec-is-equiv is-equiv-f)
      ( is-contr-retr-is-equiv is-equiv-f))

  is-equiv-Prop : (f : A â†’ B) â†’ UU-Prop (l1 âŠ” l2)
  pr1 (is-equiv-Prop f) = is-equiv f
  pr2 (is-equiv-Prop f) = is-subtype-is-equiv f

  is-emb-map-equiv :
    is-emb (map-equiv {A = A} {B = B})
  is-emb-map-equiv = is-emb-pr1 is-subtype-is-equiv

  htpy-equiv : A â‰ƒ B â†’ A â‰ƒ B â†’ UU (l1 âŠ” l2)
  htpy-equiv e e' = (map-equiv e) ~ (map-equiv e')

  refl-htpy-equiv : (e : A â‰ƒ B) â†’ htpy-equiv e e
  refl-htpy-equiv e = refl-htpy

  htpy-eq-equiv : {e e' : A â‰ƒ B} (p : Id e e') â†’ htpy-equiv e e'
  htpy-eq-equiv {e = e} {.e} refl =
    refl-htpy-equiv e

  is-contr-total-htpy-equiv :
    (e : A â‰ƒ B) â†’ is-contr (Î£ (A â‰ƒ B) (Î» e' â†’ htpy-equiv e e'))
  is-contr-total-htpy-equiv (pair f is-equiv-f) =
    is-contr-total-Eq-substructure
      ( is-contr-total-htpy f)
      ( is-subtype-is-equiv)
      ( f)
      ( refl-htpy)
      ( is-equiv-f)

  is-equiv-htpy-eq-equiv :
    (e e' : A â‰ƒ B) â†’ is-equiv (htpy-eq-equiv {e = e} {e'})
  is-equiv-htpy-eq-equiv e =
    fundamental-theorem-id e
      ( refl-htpy-equiv e)
      ( is-contr-total-htpy-equiv e)
      ( Î» e' â†’ htpy-eq-equiv {e = e} {e'})

  equiv-htpy-eq-equiv :
    (e e' : A â‰ƒ B) â†’ Id e e' â‰ƒ (htpy-equiv e e')
  pr1 (equiv-htpy-eq-equiv e e') = htpy-eq-equiv
  pr2 (equiv-htpy-eq-equiv e e') = is-equiv-htpy-eq-equiv e e'

  eq-htpy-equiv : {e e' : A â‰ƒ B} â†’ ( htpy-equiv e e') â†’ Id e e'
  eq-htpy-equiv {e = e} {e'} = map-inv-is-equiv (is-equiv-htpy-eq-equiv e e')

  Ind-htpy-equiv :
    {l3 : Level} (e : A â‰ƒ B)
    (P : (e' : A â‰ƒ B) (H : htpy-equiv e e') â†’ UU l3) â†’
    sec ( Î» (h : (e' : A â‰ƒ B) (H : htpy-equiv e e') â†’ P e' H) â†’
          h e (refl-htpy-equiv e))
  Ind-htpy-equiv {l3 = l3} e =
    Ind-identity-system l3 e
      ( refl-htpy-equiv e)
      ( is-contr-total-htpy-equiv e)
  
  ind-htpy-equiv :
    {l3 : Level} (e : A â‰ƒ B) (P : (e' : A â‰ƒ B) (H : htpy-equiv e e') â†’ UU l3) â†’
    P e (refl-htpy-equiv e) â†’ (e' : A â‰ƒ B) (H : htpy-equiv e e') â†’ P e' H
  ind-htpy-equiv e P = pr1 (Ind-htpy-equiv e P)
  
  comp-htpy-equiv :
    {l3 : Level} (e : A â‰ƒ B) (P : (e' : A â‰ƒ B) (H : htpy-equiv e e') â†’ UU l3)
    (p : P e (refl-htpy-equiv e)) â†’
    Id (ind-htpy-equiv e P p e (refl-htpy-equiv e)) p
  comp-htpy-equiv e P = pr2 (Ind-htpy-equiv e P)

  is-contr-equiv-is-contr :
    is-contr A â†’ is-contr B â†’ is-contr (A â‰ƒ B)
  pr1 (is-contr-equiv-is-contr is-contr-A is-contr-B) =
    equiv-is-contr is-contr-A is-contr-B
  pr2 (is-contr-equiv-is-contr is-contr-A is-contr-B) e =
    eq-htpy-equiv (Î» x â†’ eq-is-contr is-contr-B)

  is-trunc-equiv-is-trunc :
    (k : ğ•‹) â†’ is-trunc k A â†’ is-trunc k B â†’ is-trunc k (A â‰ƒ B)
  is-trunc-equiv-is-trunc neg-two-ğ•‹ is-trunc-A is-trunc-B =
    is-contr-equiv-is-contr is-trunc-A is-trunc-B
  is-trunc-equiv-is-trunc (succ-ğ•‹ k) is-trunc-A is-trunc-B = 
    is-trunc-Î£ (succ-ğ•‹ k)
      ( is-trunc-Î  (succ-ğ•‹ k) (Î» x â†’ is-trunc-B))
      ( Î» x â†’ is-trunc-is-prop k (is-subtype-is-equiv x))

  is-prop-equiv-is-prop : is-prop A â†’ is-prop B â†’ is-prop (A â‰ƒ B)
  is-prop-equiv-is-prop = is-trunc-equiv-is-trunc neg-one-ğ•‹

  is-set-equiv-is-set : is-set A â†’ is-set B â†’ is-set (A â‰ƒ B)
  is-set-equiv-is-set = is-trunc-equiv-is-trunc zero-ğ•‹

type-equiv-Prop :
  { l1 l2 : Level} (P : UU-Prop l1) (Q : UU-Prop l2) â†’ UU (l1 âŠ” l2)
type-equiv-Prop P Q = (type-Prop P) â‰ƒ (type-Prop Q)

equiv-Prop :
  { l1 l2 : Level} â†’ UU-Prop l1 â†’ UU-Prop l2 â†’ UU-Prop (l1 âŠ” l2)
pr1 (equiv-Prop P Q) = type-equiv-Prop P Q
pr2 (equiv-Prop P Q) =
  is-prop-equiv-is-prop (is-prop-type-Prop P) (is-prop-type-Prop Q)

type-equiv-Set :
  {l1 l2 : Level} (A : UU-Set l1) (B : UU-Set l2) â†’ UU (l1 âŠ” l2)
type-equiv-Set A B = type-Set A â‰ƒ type-Set B

equiv-Set :
  { l1 l2 : Level} â†’ UU-Set l1 â†’ UU-Set l2 â†’ UU-Set (l1 âŠ” l2)
pr1 (equiv-Set A B) = type-equiv-Set A B
pr2 (equiv-Set A B) =
  is-set-equiv-is-set (is-set-type-Set A) (is-set-type-Set B)

inv-inv-equiv :
  {i j : Level} {A : UU i} {B : UU j} (e : A â‰ƒ B) â†’
  Id (inv-equiv (inv-equiv e)) e
inv-inv-equiv (pair f (pair (pair g G) (pair h H))) = eq-htpy-equiv refl-htpy

is-equiv-inv-equiv :
  {i j : Level} {A : UU i} {B : UU j} â†’ is-equiv (inv-equiv {A = A} {B = B})
is-equiv-inv-equiv =
  is-equiv-has-inverse
    ( inv-equiv)
    ( inv-inv-equiv)
    ( inv-inv-equiv)

equiv-inv-equiv :
  {i j : Level} {A : UU i} {B : UU j} â†’ (A â‰ƒ B) â‰ƒ (B â‰ƒ A)
pr1 equiv-inv-equiv = inv-equiv
pr2 equiv-inv-equiv = is-equiv-inv-equiv

compose-inv-equiv-compose-equiv :
  {l1 l2 l3 : Level} {A : UU l1} {B : UU l2} {C : UU l3}
  (f : B â‰ƒ C) (e : A â‰ƒ B) â†’
  Id (inv-equiv f âˆ˜e (f âˆ˜e e)) e
compose-inv-equiv-compose-equiv f e =
  eq-htpy-equiv (Î» x â†’ isretr-map-inv-equiv f (map-equiv e x))

compose-equiv-compose-inv-equiv :
  {l1 l2 l3 : Level} {A : UU l1} {B : UU l2} {C : UU l3}
  (f : B â‰ƒ C) (e : A â‰ƒ C) â†’
  Id (f âˆ˜e (inv-equiv f âˆ˜e e)) e
compose-equiv-compose-inv-equiv f e =
  eq-htpy-equiv (Î» x â†’ issec-map-inv-equiv f (map-equiv e x))

is-equiv-comp-equiv :
  {l1 l2 l3 : Level} {B : UU l2} {C : UU l3}
  (f : B â‰ƒ C) (A : UU l1) â†’ is-equiv (Î» (e : A â‰ƒ B) â†’ f âˆ˜e e)
is-equiv-comp-equiv f A =
  is-equiv-has-inverse
    ( Î» e â†’ inv-equiv f âˆ˜e e)
    ( compose-equiv-compose-inv-equiv f)
    ( compose-inv-equiv-compose-equiv f)

equiv-postcomp-equiv :
  {l1 l2 l3 : Level} {B : UU l2} {C : UU l3} â†’
  (f : B â‰ƒ C) â†’ (A : UU l1) â†’ (A â‰ƒ B) â‰ƒ (A â‰ƒ C)
pr1 (equiv-postcomp-equiv f A) e = f âˆ˜e e
pr2 (equiv-postcomp-equiv f A) = is-equiv-comp-equiv f A

_â‡”_ :
  {l1 l2 : Level} â†’ UU-Prop l1 â†’ UU-Prop l2 â†’ UU (l1 âŠ” l2)
P â‡” Q = (pr1 P â†’ pr1 Q) Ã— (pr1 Q â†’ pr1 P)

equiv-iff' :
  {l1 l2 : Level} (P : UU-Prop l1) (Q : UU-Prop l2) â†’
  (P â‡” Q) â†’ (pr1 P â‰ƒ pr1 Q)
pr1 (equiv-iff' P Q t) = pr1 t
pr2 (equiv-iff' P Q t) = is-equiv-is-prop (pr2 P) (pr2 Q) (pr2 t)

equiv-iff :
  {l1 l2 : Level} (P : UU-Prop l1) (Q : UU-Prop l2) â†’
  (type-hom-Prop P Q) â†’ (type-hom-Prop Q P) â†’ pr1 P â‰ƒ pr1 Q
equiv-iff P Q f g = equiv-iff' P Q (pair f g)

iff-equiv :
  {l1 l2 : Level} (P : UU-Prop l1) (Q : UU-Prop l2) â†’
  (pr1 P â‰ƒ pr1 Q) â†’ (P â‡” Q)
pr1 (iff-equiv P Q equiv-PQ) = pr1 equiv-PQ
pr2 (iff-equiv P Q equiv-PQ) = map-inv-is-equiv (pr2 equiv-PQ)

is-prop-iff :
  {l1 l2 : Level} (P : UU-Prop l1) (Q : UU-Prop l2) â†’ is-prop (P â‡” Q)
is-prop-iff P Q =
  is-prop-prod
    ( is-prop-function-type (pr2 Q))
    ( is-prop-function-type (pr2 P))

is-prop-equiv-Prop :
  {l1 l2 : Level} (P : UU-Prop l1) (Q : UU-Prop l2) â†’
  is-prop ((pr1 P) â‰ƒ (pr1 Q))
is-prop-equiv-Prop P Q =
  is-prop-equiv-is-prop (pr2 P) (pr2 Q)

is-equiv-equiv-iff :
  {l1 l2 : Level} (P : UU-Prop l1) (Q : UU-Prop l2) â†’
  is-equiv (equiv-iff' P Q)
is-equiv-equiv-iff P Q =
  is-equiv-is-prop
    ( is-prop-iff P Q)
    ( is-prop-equiv-Prop P Q)
    ( iff-equiv P Q)

equiv-equiv-iff :
  {l1 l2 : Level} (P : UU-Prop l1) (Q : UU-Prop l2) â†’
  (P â‡” Q) â‰ƒ (type-Prop P â‰ƒ type-Prop Q)
pr1 (equiv-equiv-iff P Q) = equiv-iff' P Q
pr2 (equiv-equiv-iff P Q) = is-equiv-equiv-iff P Q

is-prop-is-contr-endomaps :
  {l : Level} (P : UU l) â†’ is-contr (P â†’ P) â†’ is-prop P
is-prop-is-contr-endomaps P H =
  is-prop-all-elements-equal (Î» x â†’ htpy-eq (eq-is-contr H))

is-contr-endomaps-is-prop :
  {l : Level} (P : UU l) â†’ is-prop P â†’ is-contr (P â†’ P)
is-contr-endomaps-is-prop P is-prop-P =
  is-proof-irrelevant-is-prop (is-prop-function-type is-prop-P) id

is-prop-is-emb :
  {l1 l2 : Level} {A : UU l1} {B : UU l2} (f : A â†’ B) â†’ is-prop (is-emb f)
is-prop-is-emb f =
  is-prop-Î  (Î» x â†’ is-prop-Î  (Î» y â†’ is-subtype-is-equiv (ap f)))

is-emb-Prop :
  {l1 l2 : Level} {A : UU l1} {B : UU l2} â†’ (A â†’ B) â†’ UU-Prop (l1 âŠ” l2)
pr1 (is-emb-Prop f) = is-emb f
pr2 (is-emb-Prop f) = is-prop-is-emb f

is-subtype-is-contr :
  {l : Level} â†’ is-subtype {lsuc l} {A = UU l} is-contr
is-subtype-is-contr A =
  is-prop-is-proof-irrelevant
    ( Î» is-contr-A â†’
      is-contr-Î£
        ( is-contr-A)
        ( Î» x â†’ is-contr-Î  (is-prop-is-contr is-contr-A x)))

is-contr-Prop : {l : Level} â†’ UU l â†’ UU-Prop l
pr1 (is-contr-Prop A) = is-contr A
pr2 (is-contr-Prop A) = is-subtype-is-contr A

is-prop-is-trunc :
  {l : Level} (k : ğ•‹) (A : UU l) â†’ is-prop (is-trunc k A)
is-prop-is-trunc neg-two-ğ•‹ = is-subtype-is-contr
is-prop-is-trunc (succ-ğ•‹ k) A =
  is-prop-Î  (Î» x â†’ is-prop-Î  (Î» y â†’ is-prop-is-trunc k (Id x y)))

is-trunc-Prop : {l : Level} (k : ğ•‹) (A : UU l) â†’ UU-Prop l
pr1 (is-trunc-Prop k A) = is-trunc k A
pr2 (is-trunc-Prop k A) = is-prop-is-trunc k A

is-prop-is-prop :
  {l : Level} (A : UU l) â†’ is-prop (is-prop A)
is-prop-is-prop = is-prop-is-trunc neg-one-ğ•‹

is-prop-Prop : {l : Level} (A : UU l) â†’ UU-Prop l
pr1 (is-prop-Prop A) = is-prop A
pr2 (is-prop-Prop A) = is-prop-is-prop A

is-prop-is-set :
  {l : Level} (A : UU l) â†’ is-prop (is-set A)
is-prop-is-set = is-prop-is-trunc zero-ğ•‹

is-set-Prop : {l : Level} â†’ UU l â†’ UU-Prop l
pr1 (is-set-Prop A) = is-set A
pr2 (is-set-Prop A) = is-prop-is-set A

is-prop-is-trunc-map :
  (k : ğ•‹) {l1 l2 : Level} {A : UU l1} {B : UU l2} (f : A â†’ B) â†’
  is-prop (is-trunc-map k f)
is-prop-is-trunc-map k f = is-prop-Î  (Î» x â†’ is-prop-is-trunc k (fib f x))

is-prop-is-contr-map :
  {l1 l2 : Level} {A : UU l1} {B : UU l2} (f : A â†’ B) â†’
  is-prop (is-contr-map f)
is-prop-is-contr-map f = is-prop-is-trunc-map neg-two-ğ•‹ f

is-prop-is-prop-map :
  {l1 l2 : Level} {A : UU l1} {B : UU l2} (f : A â†’ B) â†’ is-prop (is-prop-map f)
is-prop-is-prop-map f = is-prop-is-trunc-map neg-one-ğ•‹ f

is-trunc-map-Prop :
  (k : ğ•‹) {l1 l2 : Level} {A : UU l1} {B : UU l2} â†’ (A â†’ B) â†’ UU-Prop (l1 âŠ” l2)
pr1 (is-trunc-map-Prop k f) = is-trunc-map k f
pr2 (is-trunc-map-Prop k f) = is-prop-is-trunc-map k f

is-contr-map-Prop :
  {l1 l2 : Level} {A : UU l1} {B : UU l2} â†’ (A â†’ B) â†’ UU-Prop (l1 âŠ” l2)
pr1 (is-contr-map-Prop f) = is-contr-map f
pr2 (is-contr-map-Prop f) = is-prop-is-contr-map f

is-prop-map-Prop :
  {l1 l2 : Level} {A : UU l1} {B : UU l2} â†’ (A â†’ B) â†’ UU-Prop (l1 âŠ” l2)
pr1 (is-prop-map-Prop f) = is-prop-map f
pr2 (is-prop-map-Prop f) = is-prop-is-prop-map f

equiv-is-equiv-is-contr-map :
  {l1 l2 : Level} {A : UU l1} {B : UU l2} (f : A â†’ B) â†’
  is-contr-map f â‰ƒ is-equiv f
equiv-is-equiv-is-contr-map f =
  equiv-iff
    ( is-contr-map-Prop f)
    ( is-equiv-Prop f)
    ( is-equiv-is-contr-map)
    ( is-contr-map-is-equiv)

equiv-is-contr-map-is-equiv :
  {l1 l2 : Level} {A : UU l1} {B : UU l2} (f : A â†’ B) â†’
  is-equiv f â‰ƒ is-contr-map f
equiv-is-contr-map-is-equiv f =
  equiv-iff
    ( is-equiv-Prop f)
    ( is-contr-map-Prop f)
    ( is-contr-map-is-equiv)
    ( is-equiv-is-contr-map)

equiv-is-emb-is-prop-map :
  {l1 l2 : Level} {A : UU l1} {B : UU l2} (f : A â†’ B) â†’
  is-prop-map f â‰ƒ is-emb f
equiv-is-emb-is-prop-map f =
  equiv-iff
    ( is-prop-map-Prop f)
    ( is-emb-Prop f)
    ( is-emb-is-prop-map)
    ( is-prop-map-is-emb)

equiv-is-prop-map-is-emb :
  {l1 l2 : Level} {A : UU l1} {B : UU l2} (f : A â†’ B) â†’
  is-emb f â‰ƒ is-prop-map f
equiv-is-prop-map-is-emb f =
  equiv-iff
    ( is-emb-Prop f)
    ( is-prop-map-Prop f)
    ( is-prop-map-is-emb)
    ( is-emb-is-prop-map)

equiv-subtype-equiv :
  {l1 l2 l3 l4 : Level}
  {A : UU l1} {B : UU l2} (e : A â‰ƒ B)
  (C : A â†’ UU-Prop l3) (D : B â†’ UU-Prop l4) â†’
  ((x : A) â†’ type-Prop (C x) â†” type-Prop (D (map-equiv e x))) â†’
  total-subtype C â‰ƒ total-subtype D
equiv-subtype-equiv e C D H =
  equiv-Î£ (Î» y â†’ type-Prop (D y)) e
    ( Î» x â†’ equiv-iff' (C x) (D (map-equiv e x)) (H x))

equiv-precomp-equiv :
  {l1 l2 l3 : Level} {A : UU l1} {B : UU l2} â†’
  (A â‰ƒ B) â†’ (C : UU l3) â†’ (B â‰ƒ C) â‰ƒ (A â‰ƒ C)
equiv-precomp-equiv e C =
  equiv-subtype-equiv
    ( equiv-precomp e C)
    ( is-equiv-Prop)
    ( is-equiv-Prop)
    ( Î» g â†’
      pair
        ( is-equiv-comp' g (map-equiv e) (is-equiv-map-equiv e))
        ( Î» is-equiv-eg â†’
          is-equiv-left-factor'
            g (map-equiv e) is-equiv-eg (is-equiv-map-equiv e)))

module _
  {l1 : Level} {A : UU l1}
  where

  ev-point : {l2 : Level} (a : A) {P : A â†’ UU l2} â†’ ((x : A) â†’ P x) â†’ P a
  ev-point a f = f a

  ev-point' : {l2 : Level} (a : A) {X : UU l2} â†’ (A â†’ X) â†’ X
  ev-point' a f = f a

  dependent-universal-property-contr : (l : Level) (a : A) â†’ UU (l1 âŠ” lsuc l)
  dependent-universal-property-contr l a =
    (P : A â†’ UU l) â†’ is-equiv (ev-point a {P})

  universal-property-contr : (l : Level) (a : A) â†’ UU (l1 âŠ” lsuc l)
  universal-property-contr l a =
    (X : UU l) â†’ is-equiv (ev-point' a {X})

  universal-property-dependent-universal-property-contr :
    (a : A) â†’
    ({l : Level} â†’ dependent-universal-property-contr l a) â†’
    ({l : Level} â†’ universal-property-contr l a)
  universal-property-dependent-universal-property-contr a dup-contr {l} X =
    dup-contr {l} (Î» x â†’ X)

  is-equiv-ev-point-universal-property-contr :
    (a : A) â†’ ({l : Level} â†’ universal-property-contr l a) â†’
    is-equiv (ev-point' a {A})
  is-equiv-ev-point-universal-property-contr a up-contr =
    up-contr A

  -- !! This definition seems to cause a slowdown
  is-contr-is-equiv-ev-point :
    (a : A) â†’ is-equiv (ev-point' a) â†’ is-contr A
  pr1 (is-contr-is-equiv-ev-point a H) = a
  pr2 (is-contr-is-equiv-ev-point a H) =
    htpy-eq
      ( ap
        ( pr1)
        ( eq-is-contr'
          ( is-contr-map-is-equiv H a)
          ( pair (Î» x â†’ a) refl)
          ( pair id refl)))
  
  is-contr-universal-property-contr :
    (a : A) â†’
    ({l : Level} â†’ universal-property-contr l a) â†’ is-contr A
  is-contr-universal-property-contr a up-contr =
    is-contr-is-equiv-ev-point a
      ( is-equiv-ev-point-universal-property-contr a up-contr)

  is-contr-dependent-universal-property-contr :
    (a : A) â†’
    ({l : Level} â†’ dependent-universal-property-contr l a) â†’ is-contr A
  is-contr-dependent-universal-property-contr a dup-contr =
    is-contr-universal-property-contr a
      ( universal-property-dependent-universal-property-contr a dup-contr)

  dependent-universal-property-contr-is-contr :
    (a : A) â†’ is-contr A â†’
    {l : Level} â†’ dependent-universal-property-contr l a
  dependent-universal-property-contr-is-contr a H {l} P =
    is-equiv-has-inverse
      ( ind-singleton-is-contr a H P)
      ( comp-singleton-is-contr a H P)
      ( Î» f â†’
        eq-htpy
          ( ind-singleton-is-contr a H
            ( Î» x â†’ Id (ind-singleton-is-contr a H P (f a) x) (f x))
            ( comp-singleton-is-contr a H P (f a))))

  universal-property-contr-is-contr :
    (a : A) â†’ is-contr A â†’ {l : Level} â†’ universal-property-contr l a
  universal-property-contr-is-contr a H =
    universal-property-dependent-universal-property-contr a
      ( dependent-universal-property-contr-is-contr a H)

  equiv-universal-property-contr :
    (a : A) â†’ is-contr A â†’ {l : Level} (X : UU l) â†’ (A â†’ X) â‰ƒ X
  pr1 (equiv-universal-property-contr a H X) = ev-point' a
  pr2 (equiv-universal-property-contr a H X) =
    universal-property-contr-is-contr a H X

  is-equiv-self-diagonal-is-equiv-diagonal :
    ({l : Level} (X : UU l) â†’ is-equiv (Î» x â†’ const A X x)) â†’
    is-equiv (Î» x â†’ const A A x)
  is-equiv-self-diagonal-is-equiv-diagonal H = H A

  is-contr-is-equiv-self-diagonal :
    is-equiv (Î» x â†’ const A A x) â†’ is-contr A
  is-contr-is-equiv-self-diagonal H =
    tot (Î» x â†’ htpy-eq) (center (is-contr-map-is-equiv H id))

  is-contr-is-equiv-diagonal :
    ({l : Level} (X : UU l) â†’ is-equiv (Î» x â†’ const A X x)) â†’ is-contr A
  is-contr-is-equiv-diagonal H =
    is-contr-is-equiv-self-diagonal
      ( is-equiv-self-diagonal-is-equiv-diagonal H)

  is-equiv-diagonal-is-contr :
    is-contr A â†’
    {l : Level} (X : UU l) â†’ is-equiv (Î» x â†’ const A X x)
  is-equiv-diagonal-is-contr H X =
    is-equiv-has-inverse
      ( ev-point' (center H))
      ( Î» f â†’ eq-htpy (Î» x â†’ ap f (contraction H x)))
      ( Î» x â†’ refl)

  equiv-diagonal-is-contr :
    {l : Level} (X : UU l) â†’ is-contr A â†’ X â‰ƒ (A â†’ X)
  pr1 (equiv-diagonal-is-contr X H) = const A X
  pr2 (equiv-diagonal-is-contr X H) = is-equiv-diagonal-is-contr H X

ev-star :
  {l : Level} (P : unit â†’ UU l) â†’ ((x : unit) â†’ P x) â†’ P star
ev-star P f = f star

ev-star' :
  {l : Level} (Y : UU l) â†’ (unit â†’ Y) â†’ Y
ev-star' Y = ev-star (Î» t â†’ Y)

pt : {l1 : Level} {X : UU l1} (x : X) â†’ unit â†’ X
pt x y = x

dependent-universal-property-unit :
  {l : Level} (P : unit â†’ UU l) â†’ is-equiv (ev-star P)
dependent-universal-property-unit =
  dependent-universal-property-contr-is-contr star is-contr-unit

equiv-dependent-universal-property-unit :
  {l : Level} (P : unit â†’ UU l) â†’ ((x : unit) â†’ P x) â‰ƒ P star
pr1 (equiv-dependent-universal-property-unit P) = ev-star P
pr2 (equiv-dependent-universal-property-unit P) =
  dependent-universal-property-unit P

equiv-ev-star :
  {l : Level} (P : unit â†’ UU l) â†’ ((x : unit) â†’ P x) â‰ƒ P star
pr1 (equiv-ev-star P) = ev-star P
pr2 (equiv-ev-star P) = dependent-universal-property-unit P

universal-property-unit :
  {l : Level} (Y : UU l) â†’ is-equiv (ev-star' Y)
universal-property-unit Y = dependent-universal-property-unit (Î» t â†’ Y)

equiv-universal-property-unit :
  {l : Level} (Y : UU l) â†’ (unit â†’ Y) â‰ƒ Y
pr1 (equiv-universal-property-unit Y) = ev-star' Y
pr2 (equiv-universal-property-unit Y) = universal-property-unit Y

equiv-ev-star' :
  {l : Level} (Y : UU l) â†’ (unit â†’ Y) â‰ƒ Y
pr1 (equiv-ev-star' Y) = ev-star' Y
pr2 (equiv-ev-star' Y) = universal-property-unit Y

is-equiv-pt-is-contr :
  {l1 : Level} {X : UU l1} (x : X) â†’
  is-contr X â†’ is-equiv (pt x)
is-equiv-pt-is-contr x is-contr-X =
  is-equiv-is-contr (pt x) is-contr-unit is-contr-X

is-equiv-pt-universal-property-unit :
  {l1 : Level} (X : UU l1) (x : X) â†’
  ((l2 : Level) (Y : UU l2) â†’ is-equiv (Î» (f : X â†’ Y) â†’ f x)) â†’
  is-equiv (pt x)
is-equiv-pt-universal-property-unit X x H =
  is-equiv-is-equiv-precomp
    ( pt x)
    ( Î» l Y â†’ is-equiv-right-factor'
      ( ev-star' Y)
      ( precomp (pt x) Y)
      ( universal-property-unit Y)
      ( H _ Y))

universal-property-unit-is-equiv-pt :
  {l1 : Level} {X : UU l1} (x : X) â†’
  is-equiv (pt x) â†’
  ({l2 : Level} (Y : UU l2) â†’ is-equiv (Î» (f : X â†’ Y) â†’ f x))
universal-property-unit-is-equiv-pt x is-equiv-pt Y =
  is-equiv-comp
    ( Î» f â†’ f x)
    ( ev-star' Y)
    ( precomp (pt x) Y)
    ( Î» f â†’ refl)
    ( is-equiv-precomp-is-equiv (pt x) is-equiv-pt Y)
    ( universal-property-unit Y)

universal-property-unit-is-contr :
  {l1 : Level} {X : UU l1} (x : X) â†’
  is-contr X â†’
  ({l2 : Level} (Y : UU l2) â†’ is-equiv (Î» (f : X â†’ Y) â†’ f x))
universal-property-unit-is-contr x is-contr-X =
  universal-property-unit-is-equiv-pt x
    ( is-equiv-pt-is-contr x is-contr-X)

is-equiv-diagonal-is-equiv-pt :
  {l1 : Level} {X : UU l1} (x : X) â†’
  is-equiv (pt x) â†’
  ({l2 : Level} (Y : UU l2) â†’ is-equiv (Î» y â†’ const X Y y))
is-equiv-diagonal-is-equiv-pt {X = X} x is-equiv-pt Y =
  is-equiv-is-section-is-equiv
    ( universal-property-unit-is-equiv-pt x is-equiv-pt Y)
    ( refl-htpy)

module _
  {l1 : Level} (A : UU l1)
  where

  dependent-universal-property-empty : (l : Level) â†’ UU (l1 âŠ” lsuc l)
  dependent-universal-property-empty l =
    (P : A â†’ UU l) â†’ is-contr ((x : A) â†’ P x)

  universal-property-empty : (l : Level) â†’ UU (l1 âŠ” lsuc l)
  universal-property-empty l = (X : UU l) â†’ is-contr (A â†’ X)

  universal-property-dependent-universal-property-empty :
    ({l : Level} â†’ dependent-universal-property-empty l) â†’
    ({l : Level} â†’ universal-property-empty l)
  universal-property-dependent-universal-property-empty dup-empty {l} X =
    dup-empty {l} (Î» a â†’ X)

  is-empty-universal-property-empty :
    ({l : Level} â†’ universal-property-empty l) â†’ is-empty A
  is-empty-universal-property-empty up-empty = center (up-empty empty)

  dependent-universal-property-empty-is-empty :
    {l : Level} (H : is-empty A) â†’ dependent-universal-property-empty l
  pr1 (dependent-universal-property-empty-is-empty {l} H P) x = ex-falso (H x)
  pr2 (dependent-universal-property-empty-is-empty {l} H P) f =
    eq-htpy (Î» x â†’ ex-falso (H x))

  universal-property-empty-is-empty :
    {l : Level} (H : is-empty A) â†’ universal-property-empty l
  universal-property-empty-is-empty {l} H =
    universal-property-dependent-universal-property-empty
      ( dependent-universal-property-empty-is-empty H)

dependent-universal-property-empty' :
  {l : Level} (P : empty â†’ UU l) â†’ is-contr ((x : empty) â†’ P x)
pr1 (dependent-universal-property-empty' P) ()
pr2 (dependent-universal-property-empty' P) f = eq-htpy ind-empty

all-elements-equal-coprod :
  {l1 l2 : Level} {P : UU l1} {Q : UU l2} â†’
  (P â†’ Â¬ Q) â†’ all-elements-equal P â†’ all-elements-equal Q â†’
  all-elements-equal (coprod P Q)
all-elements-equal-coprod
  {P = P} {Q = Q} f is-prop-P is-prop-Q (inl p) (inl p') =
  ap inl (is-prop-P p p')
all-elements-equal-coprod
  {P = P} {Q = Q} f is-prop-P is-prop-Q (inl p) (inr q') =
  ex-falso (f p q')
all-elements-equal-coprod
  {P = P} {Q = Q} f is-prop-P is-prop-Q (inr q) (inl p') =
  ex-falso (f p' q)
all-elements-equal-coprod
  {P = P} {Q = Q} f is-prop-P is-prop-Q (inr q) (inr q') =
  ap inr (is-prop-Q q q')

is-prop-coprod :
  {l1 l2 : Level} {P : UU l1} {Q : UU l2} â†’
  (P â†’ Â¬ Q) â†’ is-prop P â†’ is-prop Q â†’ is-prop (coprod P Q)
is-prop-coprod f is-prop-P is-prop-Q =
  is-prop-all-elements-equal
    ( all-elements-equal-coprod f
      ( eq-is-prop' is-prop-P)
      ( eq-is-prop' is-prop-Q))

is-prop-neg : {l : Level} {A : UU l} â†’ is-prop (Â¬ A)
is-prop-neg {A = A} = is-prop-function-type is-prop-empty

type-Î -Set' :
  {l1 l2 : Level} (A : UU l1) (B : A â†’ UU-Set l2) â†’ UU (l1 âŠ” l2)
type-Î -Set' A B = (x : A) â†’ type-Set (B x)

is-set-type-Î -Set' :
  {l1 l2 : Level} (A : UU l1) (B : A â†’ UU-Set l2) â†’ is-set (type-Î -Set' A B)
is-set-type-Î -Set' A B =
  is-set-Î  (Î» x â†’ is-set-type-Set (B x))

Î -Set' :
  {l1 l2 : Level} (A : UU l1) (B : A â†’ UU-Set l2) â†’ UU-Set (l1 âŠ” l2)
pr1 (Î -Set' A B) = type-Î -Set' A B
pr2 (Î -Set' A B) = is-set-type-Î -Set' A B

type-Î -Set :
  {l1 l2 : Level} (A : UU-Set l1) (B : type-Set A â†’ UU-Set l2) â†’ UU (l1 âŠ” l2)
type-Î -Set A B = type-Î -Set' (type-Set A) B

is-set-type-Î -Set :
  {l1 l2 : Level} (A : UU-Set l1) (B : type-Set A â†’ UU-Set l2) â†’
  is-set (type-Î -Set A B)
is-set-type-Î -Set A B =
  is-set-type-Î -Set' (type-Set A) B

Î -Set :
  {l1 l2 : Level} (A : UU-Set l1) â†’
  (type-Set A â†’ UU-Set l2) â†’ UU-Set (l1 âŠ” l2)
pr1 (Î -Set A B) = type-Î -Set A B
pr2 (Î -Set A B) = is-set-type-Î -Set A B

type-hom-Set :
  {l1 l2 : Level} â†’ UU-Set l1 â†’ UU-Set l2 â†’ UU (l1 âŠ” l2)
type-hom-Set A B = type-Set A â†’ type-Set B

is-set-type-hom-Set :
  {l1 l2 : Level} (A : UU-Set l1) (B : UU-Set l2) â†’
  is-set (type-hom-Set A B)
is-set-type-hom-Set A B = is-set-function-type (is-set-type-Set B)

hom-Set :
  {l1 l2 : Level} â†’ UU-Set l1 â†’ UU-Set l2 â†’ UU-Set (l1 âŠ” l2)
pr1 (hom-Set A B) = type-hom-Set A B
pr2 (hom-Set A B) = is-set-type-hom-Set A B

is-equiv-inv-htpy :
  {l1 l2 : Level} {A : UU l1} {B : A â†’ UU l2}
  (f g : (x : A) â†’ B x) â†’ is-equiv (inv-htpy {f = f} {g = g})
is-equiv-inv-htpy f g =
  is-equiv-has-inverse
    ( inv-htpy)
    ( Î» H â†’ eq-htpy (Î» x â†’ inv-inv (H x)))
    ( Î» H â†’ eq-htpy (Î» x â†’ inv-inv (H x)))

equiv-inv-htpy :
  {l1 l2 : Level} {A : UU l1} {B : A â†’ UU l2}
  (f g : (x : A) â†’ B x) â†’ (f ~ g) â‰ƒ (g ~ f)
pr1 (equiv-inv-htpy f g) = inv-htpy
pr2 (equiv-inv-htpy f g) = is-equiv-inv-htpy f g

is-equiv-concat-htpy :
  {l1 l2 : Level} {A : UU l1} {B : A â†’ UU l2}
  {f g : (x : A) â†’ B x} (H : f ~ g) â†’
  (h : (x : A) â†’ B x) â†’ is-equiv (concat-htpy H h)
is-equiv-concat-htpy {A = A} {B = B} {f} =
  ind-htpy f
    ( Î» g H â†’ (h : (x : A) â†’ B x) â†’ is-equiv (concat-htpy H h))
    ( Î» h â†’ is-equiv-id)

equiv-concat-htpy :
  {l1 l2 : Level} {A : UU l1} {B : A â†’ UU l2}
  {f g : (x : A) â†’ B x} (H : f ~ g) (h : (x : A) â†’ B x) â†’
  (g ~ h) â‰ƒ (f ~ h)
pr1 (equiv-concat-htpy H h) = concat-htpy H h
pr2 (equiv-concat-htpy H h) = is-equiv-concat-htpy H h

hom-slice :
  {l1 l2 l3 : Level} {X : UU l1} {A : UU l2} {B : UU l3}
  (f : A â†’ X) (g : B â†’ X) â†’ UU (l1 âŠ” (l2 âŠ” l3))
hom-slice {A = A} {B} f g = Î£ (A â†’ B) (Î» h â†’ f ~ (g âˆ˜ h))

map-hom-slice :
  {l1 l2 l3 : Level} {X : UU l1} {A : UU l2} {B : UU l3}
  (f : A â†’ X) (g : B â†’ X) â†’ hom-slice f g â†’ A â†’ B
map-hom-slice f g h = pr1 h

triangle-hom-slice :
  {l1 l2 l3 : Level} {X : UU l1} {A : UU l2} {B : UU l3}
  (f : A â†’ X) (g : B â†’ X) (h : hom-slice f g) â†’
  f ~ (g âˆ˜ (map-hom-slice f g h))
triangle-hom-slice f g h = pr2 h

module _
  {l1 l2 l3 : Level} {X : UU l1} {A : UU l2} {B : UU l3}
  (f : A â†’ X) (g : B â†’ X)
  where

  htpy-hom-slice : (h h' : hom-slice f g) â†’ UU (l1 âŠ” l2 âŠ” l3)
  htpy-hom-slice h h' =
    Î£ ( map-hom-slice f g h ~ map-hom-slice f g h')
      ( Î» K â†’
        ( (triangle-hom-slice f g h) âˆ™h (g Â·l K)) ~
        ( triangle-hom-slice f g h'))

  refl-htpy-hom-slice : (h : hom-slice f g) â†’ htpy-hom-slice h h
  pr1 (refl-htpy-hom-slice h) = refl-htpy
  pr2 (refl-htpy-hom-slice h) = right-unit-htpy
  
  htpy-eq-hom-slice : (h h' : hom-slice f g) â†’ Id h h' â†’ htpy-hom-slice h h'
  htpy-eq-hom-slice h .h refl = refl-htpy-hom-slice h

  is-contr-total-htpy-hom-slice :
    (h : hom-slice f g) â†’ is-contr (Î£ (hom-slice f g) (htpy-hom-slice h))
  is-contr-total-htpy-hom-slice h =
    is-contr-total-Eq-structure
      ( Î» h' H' K â†’ ((triangle-hom-slice f g h) âˆ™h (g Â·l K)) ~ H')
      ( is-contr-total-htpy (map-hom-slice f g h))
      ( pair (map-hom-slice f g h) refl-htpy)
      ( is-contr-equiv'
        ( Î£ ( f ~ (g âˆ˜ (map-hom-slice f g h)))
            ( Î» H' â†’ (triangle-hom-slice f g h) ~ H'))
        ( equiv-tot (equiv-concat-htpy right-unit-htpy))
        ( is-contr-total-htpy (triangle-hom-slice f g h)))

  is-equiv-htpy-eq-hom-slice :
    (h h' : hom-slice f g) â†’ is-equiv (htpy-eq-hom-slice h h')
  is-equiv-htpy-eq-hom-slice h =
    fundamental-theorem-id h
      ( refl-htpy-hom-slice h)
      ( is-contr-total-htpy-hom-slice h)
      ( htpy-eq-hom-slice h)

  equiv-htpy-eq-hom-slice :
    (h h' : hom-slice f g) â†’ Id h h' â‰ƒ htpy-hom-slice h h'
  pr1 (equiv-htpy-eq-hom-slice h h') = htpy-eq-hom-slice h h'
  pr2 (equiv-htpy-eq-hom-slice h h') = is-equiv-htpy-eq-hom-slice h h'

  eq-htpy-hom-slice :
    (h h' : hom-slice f g) â†’ htpy-hom-slice h h' â†’ Id h h'
  eq-htpy-hom-slice h h' = map-inv-is-equiv (is-equiv-htpy-eq-hom-slice h h')

fib-triangle :
  {i j k : Level} {X : UU i} {A : UU j} {B : UU k}
  (f : A â†’ X) (g : B â†’ X) (h : A â†’ B) (H : f ~ (g âˆ˜ h))
  (x : X) â†’ (fib f x) â†’ (fib g x)
pr1 (fib-triangle f g h H .(f a) (pair a refl)) = h a
pr2 (fib-triangle f g h H .(f a) (pair a refl)) = inv (H a)

fib-triangle' :
  {i j k : Level} {X : UU i} {A : UU j} {B : UU k}
  (g : B â†’ X) (h : A â†’ B) (x : X) â†’ (fib (g âˆ˜ h) x) â†’ fib g x
fib-triangle' g h x = fib-triangle (g âˆ˜ h) g h refl-htpy x

square-tot-fib-triangle :
  {i j k : Level} {X : UU i} {A : UU j} {B : UU k}
  (f : A â†’ X) (g : B â†’ X) (h : A â†’ B) (H : f ~ (g âˆ˜ h)) â†’
  (h âˆ˜ (map-equiv-total-fib f)) ~
  ((map-equiv-total-fib g) âˆ˜ (tot (fib-triangle f g h H)))
square-tot-fib-triangle f g h H (pair .(f a) (pair a refl)) = refl

is-equiv-top-is-equiv-bottom-square :
  {l1 l2 l3 l4 : Level} {A : UU l1} {B : UU l2} {C : UU l3} {D : UU l4}
  (f : A â†’ B) (g : C â†’ D) (h : A â†’ C) (i : B â†’ D) (H : (i âˆ˜ f) ~ (g âˆ˜ h)) â†’
  (is-equiv f) â†’ (is-equiv g) â†’ (is-equiv i) â†’ (is-equiv h)
is-equiv-top-is-equiv-bottom-square
  f g h i H is-equiv-f is-equiv-g is-equiv-i =
  is-equiv-right-factor (i âˆ˜ f) g h H is-equiv-g
    ( is-equiv-comp' i f is-equiv-f is-equiv-i)

is-equiv-bottom-is-equiv-top-square :
  {l1 l2 l3 l4 : Level} {A : UU l1} {B : UU l2} {C : UU l3} {D : UU l4}
  (f : A â†’ B) (g : C â†’ D) (h : A â†’ C) (i : B â†’ D) (H : (i âˆ˜ f) ~ (g âˆ˜ h)) â†’
  (is-equiv f) â†’ (is-equiv g) â†’ (is-equiv h) â†’ (is-equiv i)
is-equiv-bottom-is-equiv-top-square
  f g h i H is-equiv-f is-equiv-g is-equiv-h = 
  is-equiv-left-factor' i f
    ( is-equiv-comp (i âˆ˜ f) g h H is-equiv-h is-equiv-g) is-equiv-f

is-equiv-left-is-equiv-right-square :
  {l1 l2 l3 l4 : Level} {A : UU l1} {B : UU l2} {C : UU l3} {D : UU l4}
  (f : A â†’ B) (g : C â†’ D) (h : A â†’ C) (i : B â†’ D) (H : (i âˆ˜ f) ~ (g âˆ˜ h)) â†’
  (is-equiv h) â†’ (is-equiv i) â†’ (is-equiv g) â†’ (is-equiv f)
is-equiv-left-is-equiv-right-square
  f g h i H is-equiv-h is-equiv-i is-equiv-g =
  is-equiv-right-factor' i f is-equiv-i
    ( is-equiv-comp (i âˆ˜ f) g h H is-equiv-h is-equiv-g)

is-equiv-right-is-equiv-left-square :
  {l1 l2 l3 l4 : Level} {A : UU l1} {B : UU l2} {C : UU l3} {D : UU l4}
  (f : A â†’ B) (g : C â†’ D) (h : A â†’ C) (i : B â†’ D) (H : (i âˆ˜ f) ~ (g âˆ˜ h)) â†’
  (is-equiv h) â†’ (is-equiv i) â†’ (is-equiv f) â†’ (is-equiv g)
is-equiv-right-is-equiv-left-square
  f g h i H is-equiv-h is-equiv-i is-equiv-f =
  is-equiv-left-factor (i âˆ˜ f) g h H
    ( is-equiv-comp' i f is-equiv-f is-equiv-i)
    ( is-equiv-h)

is-fiberwise-equiv-is-equiv-triangle :
  {i j k : Level} {X : UU i} {A : UU j} {B : UU k}
  (f : A â†’ X) (g : B â†’ X) (h : A â†’ B) (H : f ~ (g âˆ˜ h)) â†’
  (E : is-equiv h) â†’ is-fiberwise-equiv (fib-triangle f g h H)
is-fiberwise-equiv-is-equiv-triangle f g h H E =
  is-fiberwise-equiv-is-equiv-tot
    ( fib-triangle f g h H)
    ( is-equiv-top-is-equiv-bottom-square
      ( map-equiv-total-fib f)
      ( map-equiv-total-fib g)
      ( tot (fib-triangle f g h H))
      ( h)
      ( square-tot-fib-triangle f g h H)
      ( is-equiv-map-equiv-total-fib f)
      ( is-equiv-map-equiv-total-fib g)
      ( E))

is-equiv-triangle-is-fiberwise-equiv :
  {i j k : Level} {X : UU i} {A : UU j} {B : UU k}
  (f : A â†’ X) (g : B â†’ X) (h : A â†’ B) (H : f ~ (g âˆ˜ h)) â†’
  (E : is-fiberwise-equiv (fib-triangle f g h H)) â†’ is-equiv h
is-equiv-triangle-is-fiberwise-equiv f g h H E =
  is-equiv-bottom-is-equiv-top-square
    ( map-equiv-total-fib f)
    ( map-equiv-total-fib g)
    ( tot (fib-triangle f g h H))
    ( h)
    ( square-tot-fib-triangle f g h H)
    ( is-equiv-map-equiv-total-fib f)
    ( is-equiv-map-equiv-total-fib g)
    ( is-equiv-tot-is-fiberwise-equiv E)
  
fiberwise-hom-hom-slice :
  {l1 l2 l3 : Level} {X : UU l1} {A : UU l2} {B : UU l3}
  (f : A â†’ X) (g : B â†’ X) â†’
  hom-slice f g â†’ (x : X) â†’ (fib f x) â†’ (fib g x)
fiberwise-hom-hom-slice f g (pair h H) = fib-triangle f g h H

hom-slice-fiberwise-hom :
  {l1 l2 l3 : Level} {X : UU l1} {A : UU l2} {B : UU l3}
  (f : A â†’ X) (g : B â†’ X) â†’
  ((x : X) â†’ (fib f x) â†’ (fib g x)) â†’ hom-slice f g
pr1 (hom-slice-fiberwise-hom f g Î±) a = pr1 (Î± (f a) (pair a refl))
pr2 (hom-slice-fiberwise-hom f g Î±) a = inv (pr2 (Î± (f a) (pair a refl)))

issec-hom-slice-fiberwise-hom-eq-htpy :
  {l1 l2 l3 : Level} {X : UU l1} {A : UU l2} {B : UU l3}
  (f : A â†’ X) (g : B â†’ X) (Î± : (x : X) â†’ (fib f x) â†’ (fib g x)) (x : X) â†’
  (fiberwise-hom-hom-slice f g (hom-slice-fiberwise-hom f g Î±) x) ~ (Î± x)
issec-hom-slice-fiberwise-hom-eq-htpy f g Î± .(f a) (pair a refl) =
  eq-pair-Î£ refl (inv-inv (pr2 (Î± (f a) (pair a refl))))

issec-hom-slice-fiberwise-hom :
  {l1 l2 l3 : Level} {X : UU l1} {A : UU l2} {B : UU l3}
  (f : A â†’ X) (g : B â†’ X) â†’
  ((fiberwise-hom-hom-slice f g) âˆ˜ (hom-slice-fiberwise-hom f g)) ~ id
issec-hom-slice-fiberwise-hom f g Î± =
  eq-htpy (Î» x â†’ eq-htpy (issec-hom-slice-fiberwise-hom-eq-htpy f g Î± x))

isretr-hom-slice-fiberwise-hom :
  {l1 l2 l3 : Level} {X : UU l1} {A : UU l2} {B : UU l3}
  (f : A â†’ X) (g : B â†’ X) â†’
  ((hom-slice-fiberwise-hom f g) âˆ˜ (fiberwise-hom-hom-slice f g)) ~ id
isretr-hom-slice-fiberwise-hom f g (pair h H) =
  eq-pair-Î£ refl (eq-htpy (Î» a â†’ (inv-inv (H a))))

is-equiv-fiberwise-hom-hom-slice :
  {l1 l2 l3 : Level} {X : UU l1} {A : UU l2} {B : UU l3}
  (f : A â†’ X) (g : B â†’ X) â†’
  is-equiv (fiberwise-hom-hom-slice f g)
is-equiv-fiberwise-hom-hom-slice f g =
  is-equiv-has-inverse
    ( hom-slice-fiberwise-hom f g)
    ( issec-hom-slice-fiberwise-hom f g)
    ( isretr-hom-slice-fiberwise-hom f g)

equiv-fiberwise-hom-hom-slice :
  {l1 l2 l3 : Level} {X : UU l1} {A : UU l2} {B : UU l3}
  (f : A â†’ X) (g : B â†’ X) â†’
  hom-slice f g â‰ƒ ((x : X) â†’ fib f x â†’ fib g x)
pr1 (equiv-fiberwise-hom-hom-slice f g) = fiberwise-hom-hom-slice f g
pr2 (equiv-fiberwise-hom-hom-slice f g) = is-equiv-fiberwise-hom-hom-slice f g

is-equiv-hom-slice-fiberwise-hom :
  {l1 l2 l3 : Level} {X : UU l1} {A : UU l2} {B : UU l3}
  (f : A â†’ X) (g : B â†’ X) â†’
  is-equiv (hom-slice-fiberwise-hom f g)
is-equiv-hom-slice-fiberwise-hom f g =
  is-equiv-has-inverse
    ( fiberwise-hom-hom-slice f g)
    ( isretr-hom-slice-fiberwise-hom f g)
    ( issec-hom-slice-fiberwise-hom f g)

equiv-hom-slice-fiberwise-hom :
  {l1 l2 l3 : Level} {X : UU l1} {A : UU l2} {B : UU l3}
  (f : A â†’ X) (g : B â†’ X) â†’
  ((x : X) â†’ fib f x â†’ fib g x) â‰ƒ hom-slice f g
pr1 (equiv-hom-slice-fiberwise-hom f g) = hom-slice-fiberwise-hom f g
pr2 (equiv-hom-slice-fiberwise-hom f g) = is-equiv-hom-slice-fiberwise-hom f g

equiv-slice :
  {l1 l2 l3 : Level} {X : UU l1} {A : UU l2} {B : UU l3}
  (f : A â†’ X) (g : B â†’ X) â†’ UU (l1 âŠ” (l2 âŠ” l3))
equiv-slice {A = A} {B} f g = Î£ (A â‰ƒ B) (Î» e â†’ f ~ (g âˆ˜ (map-equiv e)))

hom-equiv-slice :
  {l1 l2 l3 : Level} {X : UU l1} {A : UU l2} {B : UU l3}
  (f : A â†’ X) (g : B â†’ X) â†’
  equiv-slice f g â†’ hom-slice f g
pr1 (hom-equiv-slice f g e) = pr1 (pr1 e)
pr2 (hom-equiv-slice f g e) = pr2 e

is-equiv-subtype-is-equiv :
  {l1 l2 l3 l4 : Level} {A : UU l1} {B : UU l2}
  {P : A â†’ UU l3} {Q : B â†’ UU l4}
  (is-subtype-P : is-subtype P) (is-subtype-Q : is-subtype Q)
  (f : A â†’ B) (g : (x : A) â†’ P x â†’ Q (f x)) â†’
  is-equiv f â†’ ((x : A) â†’ (Q (f x)) â†’ P x) â†’ is-equiv (map-Î£ Q f g)
is-equiv-subtype-is-equiv {Q = Q} is-subtype-P is-subtype-Q f g is-equiv-f h =
  is-equiv-map-Î£ Q f g is-equiv-f
    ( Î» x â†’ is-equiv-is-prop (is-subtype-P x) (is-subtype-Q (f x)) (h x))

is-equiv-subtype-is-equiv' :
  {l1 l2 l3 l4 : Level} {A : UU l1} {B : UU l2}
  {P : A â†’ UU l3} {Q : B â†’ UU l4}
  (is-subtype-P : is-subtype P) (is-subtype-Q : is-subtype Q)
  (f : A â†’ B) (g : (x : A) â†’ P x â†’ Q (f x)) â†’
  (is-equiv-f : is-equiv f) â†’
  ((y : B) â†’ (Q y) â†’ P (map-inv-is-equiv is-equiv-f y)) â†’
  is-equiv (map-Î£ Q f g)
is-equiv-subtype-is-equiv' {P = P} {Q}
  is-subtype-P is-subtype-Q f g is-equiv-f h =
  is-equiv-map-Î£ Q f g is-equiv-f
    ( Î» x â†’ is-equiv-is-prop (is-subtype-P x) (is-subtype-Q (f x))
      ( (tr P (isretr-map-inv-is-equiv is-equiv-f x)) âˆ˜ (h (f x))))

is-fiberwise-equiv-fiberwise-equiv-equiv-slice :
  {l1 l2 l3 : Level} {X : UU l1} {A : UU l2} {B : UU l3}
  (f : A â†’ X) (g : B â†’ X)
  (t : hom-slice f g) â†’ is-equiv (pr1 t) â†’
  is-fiberwise-equiv (fiberwise-hom-hom-slice f g t)
is-fiberwise-equiv-fiberwise-equiv-equiv-slice f g (pair h H) =
  is-fiberwise-equiv-is-equiv-triangle f g h H

left-factor-fiberwise-equiv-equiv-slice :
  {l1 l2 l3 : Level} {X : UU l1} {A : UU l2} {B : UU l3}
  (f : A â†’ X) (g : B â†’ X) â†’
  Î£ (hom-slice f g) (Î» hH â†’ is-equiv (pr1 hH)) â†’
  Î£ ((x : X) â†’ (fib f x) â†’ (fib g x)) is-fiberwise-equiv
left-factor-fiberwise-equiv-equiv-slice f g =
  map-Î£
    ( is-fiberwise-equiv)
    ( fiberwise-hom-hom-slice f g)
    ( is-fiberwise-equiv-fiberwise-equiv-equiv-slice f g)

swap-equiv-slice :
  {l1 l2 l3 : Level} {X : UU l1} {A : UU l2} {B : UU l3}
  (f : A â†’ X) (g : B â†’ X) â†’
  equiv-slice f g â†’
  Î£ (hom-slice f g) (Î» hH â†’ is-equiv (pr1 hH))
swap-equiv-slice {A = A} {B} f g =
  map-equiv-double-structure is-equiv (Î» h â†’ f ~ (g âˆ˜ h))

is-equiv-swap-equiv-slice :
  {l1 l2 l3 : Level} {X : UU l1} {A : UU l2} {B : UU l3}
  (f : A â†’ X) (g : B â†’ X) â†’
  is-equiv (swap-equiv-slice f g)
is-equiv-swap-equiv-slice f g =
  is-equiv-map-equiv (equiv-double-structure is-equiv (Î» h â†’ f ~ (g âˆ˜ h)))

fiberwise-equiv-equiv-slice :
  {l1 l2 l3 : Level} {X : UU l1} {A : UU l2} {B : UU l3}
  (f : A â†’ X) (g : B â†’ X) â†’
  equiv-slice f g â†’ Î£ ((x : X) â†’ (fib f x) â†’ (fib g x)) is-fiberwise-equiv
fiberwise-equiv-equiv-slice {X = X} {A} {B} f g =
  ( left-factor-fiberwise-equiv-equiv-slice f g) âˆ˜
  ( swap-equiv-slice f g)

is-equiv-hom-slice-is-fiberwise-equiv-fiberwise-hom-hom-slice :
  {l1 l2 l3 : Level} {X : UU l1} {A : UU l2} {B : UU l3}
  (f : A â†’ X) (g : B â†’ X) â†’
  (t : hom-slice f g) â†’
  ((x : X) â†’ is-equiv (fiberwise-hom-hom-slice f g t x)) â†’
  is-equiv (pr1 t)
is-equiv-hom-slice-is-fiberwise-equiv-fiberwise-hom-hom-slice
  f g (pair h H) =
  is-equiv-triangle-is-fiberwise-equiv f g h H

is-equiv-fiberwise-equiv-equiv-slice :
  {l1 l2 l3 : Level} {X : UU l1} {A : UU l2} {B : UU l3}
  (f : A â†’ X) (g : B â†’ X) â†’
  is-equiv (fiberwise-equiv-equiv-slice f g)
is-equiv-fiberwise-equiv-equiv-slice {X = X} {A} {B} f g =
  is-equiv-comp
    ( fiberwise-equiv-equiv-slice f g)
    ( left-factor-fiberwise-equiv-equiv-slice f g)
    ( swap-equiv-slice f g)
    ( refl-htpy)
    ( is-equiv-swap-equiv-slice f g)
    ( is-equiv-subtype-is-equiv
      ( Î» t â†’ is-subtype-is-equiv (pr1 t))
      ( Î» Î± â†’ is-prop-Î  (Î» x â†’ is-subtype-is-equiv (Î± x)))
      ( fiberwise-hom-hom-slice f g)
      ( is-fiberwise-equiv-fiberwise-equiv-equiv-slice f g)
      ( is-equiv-fiberwise-hom-hom-slice f g)
      ( is-equiv-hom-slice-is-fiberwise-equiv-fiberwise-hom-hom-slice
        f g))

equiv-fiberwise-equiv-equiv-slice :
  {l1 l2 l3 : Level} {X : UU l1} {A : UU l2} {B : UU l3}
  (f : A â†’ X) (g : B â†’ X) â†’
  equiv-slice f g â‰ƒ Î£ ((x : X) â†’ (fib f x) â†’ (fib g x)) is-fiberwise-equiv
pr1 (equiv-fiberwise-equiv-equiv-slice f g) = fiberwise-equiv-equiv-slice f g
pr2 (equiv-fiberwise-equiv-equiv-slice f g) =
  is-equiv-fiberwise-equiv-equiv-slice f g

equiv-fam-equiv-equiv-slice :
  {l1 l2 l3 : Level} {X : UU l1} {A : UU l2} {B : UU l3}
  (f : A â†’ X) (g : B â†’ X) â†’
  equiv-slice f g â‰ƒ ((x : X) â†’ (fib f x) â‰ƒ (fib g x))
equiv-fam-equiv-equiv-slice f g =
  ( equiv-inv-choice-âˆ (Î» x â†’ is-equiv)) âˆ˜e
  ( equiv-fiberwise-equiv-equiv-slice f g)

map-reduce-Î -fib :
  {l1 l2 l3 : Level} {A : UU l1} {B : UU l2} (f : A â†’ B)
  (C : (y : B) (z : fib f y) â†’ UU l3) â†’
  ((y : B) (z : fib f y) â†’ C y z) â†’ ((x : A) â†’ C (f x) (pair x refl))
map-reduce-Î -fib f C h x = h (f x) (pair x refl)

inv-map-reduce-Î -fib :
  {l1 l2 l3 : Level} {A : UU l1} {B : UU l2} (f : A â†’ B)
  (C : (y : B) (z : fib f y) â†’ UU l3) â†’
  ((x : A) â†’ C (f x) (pair x refl)) â†’ ((y : B) (z : fib f y) â†’ C y z)
inv-map-reduce-Î -fib f C h .(f x) (pair x refl) = h x

issec-inv-map-reduce-Î -fib :
  {l1 l2 l3 : Level} {A : UU l1} {B : UU l2} (f : A â†’ B)
  (C : (y : B) (z : fib f y) â†’ UU l3) â†’
  ((map-reduce-Î -fib f C) âˆ˜ (inv-map-reduce-Î -fib f C)) ~ id
issec-inv-map-reduce-Î -fib f C h = refl

isretr-inv-map-reduce-Î -fib' :
  {l1 l2 l3 : Level} {A : UU l1} {B : UU l2} (f : A â†’ B)
  (C : (y : B) (z : fib f y) â†’ UU l3) â†’
  (h : (y : B) (z : fib f y) â†’ C y z) (y : B) â†’
  (inv-map-reduce-Î -fib f C ((map-reduce-Î -fib f C) h) y) ~ (h y)
isretr-inv-map-reduce-Î -fib' f C h .(f z) (pair z refl) = refl

isretr-inv-map-reduce-Î -fib :
  {l1 l2 l3 : Level} {A : UU l1} {B : UU l2} (f : A â†’ B)
  (C : (y : B) (z : fib f y) â†’ UU l3) â†’
  ((inv-map-reduce-Î -fib f C) âˆ˜ (map-reduce-Î -fib f C)) ~ id
isretr-inv-map-reduce-Î -fib f C h =
  eq-htpy (Î» y â†’ eq-htpy (isretr-inv-map-reduce-Î -fib' f C h y))

is-equiv-map-reduce-Î -fib :
  {l1 l2 l3 : Level} {A : UU l1} {B : UU l2} (f : A â†’ B) â†’
  (C : (y : B) (z : fib f y) â†’ UU l3) â†’
  is-equiv (map-reduce-Î -fib f C)
is-equiv-map-reduce-Î -fib f C =
  is-equiv-has-inverse
    ( inv-map-reduce-Î -fib f C)
    ( issec-inv-map-reduce-Î -fib f C)
    ( isretr-inv-map-reduce-Î -fib f C)

reduce-Î -fib' :
  {l1 l2 l3 : Level} {A : UU l1} {B : UU l2} (f : A â†’ B) â†’
  (C : (y : B) (z : fib f y) â†’ UU l3) â†’
  ((y : B) (z : fib f y) â†’ C y z) â‰ƒ ((x : A) â†’ C (f x) (pair x refl))
pr1 (reduce-Î -fib' f C) = map-reduce-Î -fib f C
pr2 (reduce-Î -fib' f C) = is-equiv-map-reduce-Î -fib f C

reduce-Î -fib :
  {l1 l2 l3 : Level} {A : UU l1} {B : UU l2} (f : A â†’ B) â†’
  (C : B â†’ UU l3) â†’ ((y : B) â†’ fib f y â†’ C y) â‰ƒ ((x : A) â†’ C (f x))
reduce-Î -fib f C = reduce-Î -fib' f (Î» y z â†’ C y)

equiv-fib-map-Î ' :
  {l1 l2 l3 l4 : Level} {I : UU l1} {A : I â†’ UU l2} {B : I â†’ UU l3}
  {J : UU l4} (Î± : J â†’ I) (f : (i : I) â†’ A i â†’ B i)
  (h : (j : J) â†’ B (Î± j)) â†’
  ((j : J) â†’ fib (f (Î± j)) (h j)) â‰ƒ fib (map-Î ' Î± f) h
equiv-fib-map-Î ' Î± f h =
  equiv-tot (Î» x â†’ equiv-eq-htpy) âˆ˜e equiv-choice-âˆ

is-trunc-map-map-Î -is-trunc-map' :
  (k : ğ•‹) {l1 l2 l3 l4 : Level} {I : UU l1} {A : I â†’ UU l2} {B : I â†’ UU l3}
  {J : UU l4} (Î± : J â†’ I) (f : (i : I) â†’ A i â†’ B i) â†’
  ((i : I) â†’ is-trunc-map k (f i)) â†’ is-trunc-map k (map-Î ' Î± f)
is-trunc-map-map-Î -is-trunc-map' k {J = J} Î± f H h =
  is-trunc-equiv' k
    ( (j : J) â†’ fib (f (Î± j)) (h j))
    ( equiv-fib-map-Î ' Î± f h)
    ( is-trunc-Î  k (Î» j â†’ H (Î± j) (h j)))

is-trunc-map-is-trunc-map-map-Î ' :
  (k : ğ•‹) {l1 l2 l3 : Level} {I : UU l1} {A : I â†’ UU l2} {B : I â†’ UU l3}
  (f : (i : I) â†’ A i â†’ B i) â†’
  ({l : Level} {J : UU l} (Î± : J â†’ I) â†’ is-trunc-map k (map-Î ' Î± f)) â†’
  (i : I) â†’ is-trunc-map k (f i)
is-trunc-map-is-trunc-map-map-Î ' k {A = A} {B} f H i b =
  is-trunc-equiv' k
    ( fib (map-Î  (Î» (x : unit) â†’ f i)) (const unit (B i) b))
    ( equiv-Î£
      ( Î» a â†’ Id (f i a) b)
      ( equiv-universal-property-unit (A i))
      ( Î» h â†’ equiv-ap
        ( equiv-universal-property-unit (B i))
        ( map-Î  (Î» x â†’ f i) h)
        ( const unit (B i) b)))
    ( H (Î» x â†’ i) (const unit (B i) b))

is-trunc-map-postcomp-is-trunc-map :
  {l1 l2 l3 : Level} (k : ğ•‹) (A : UU l3) {X : UU l1} {Y : UU l2} (f : X â†’ Y) â†’
  is-trunc-map k f â†’ is-trunc-map k (postcomp A f)
is-trunc-map-postcomp-is-trunc-map k A {X} {Y} f is-trunc-f =
  is-trunc-map-map-Î -is-trunc-map' k
    ( const A unit star)
    ( const unit (X â†’ Y) f)
    ( const unit (is-trunc-map k f) is-trunc-f)

is-trunc-map-is-trunc-map-postcomp :
  {l1 l2 : Level} (k : ğ•‹) {X : UU l1} {Y : UU l2} (f : X â†’ Y) â†’
  ( {l3 : Level} (A : UU l3) â†’ is-trunc-map k (postcomp A f)) â†’
  is-trunc-map k f
is-trunc-map-is-trunc-map-postcomp k {X} {Y} f is-trunc-post-f =
  is-trunc-map-is-trunc-map-map-Î ' k
    ( const unit (X â†’ Y) f)
    ( Î» {l} {J} Î± â†’ is-trunc-post-f {l} J)
    ( star)

is-equiv-is-equiv-postcomp :
  {l1 l2 : Level} {X : UU l1} {Y : UU l2} (f : X â†’ Y) â†’
  ({l3 : Level} (A : UU l3) â†’ is-equiv (postcomp A f)) â†’ is-equiv f
is-equiv-is-equiv-postcomp {X = X} {Y = Y} f post-comp-equiv-f =
  is-equiv-is-contr-map
    ( is-trunc-map-is-trunc-map-postcomp neg-two-ğ•‹ f
      ( Î» {l} A â†’ is-contr-map-is-equiv (post-comp-equiv-f A)))

is-equiv-is-equiv-postcomp' :
  {l : Level} {X : UU l} {Y : UU l} (f : X â†’ Y) â†’
  ((A : UU l) â†’ is-equiv (postcomp A f)) â†’ is-equiv f
is-equiv-is-equiv-postcomp'
  {l} {X} {Y} f is-equiv-postcomp-f =
  let sec-f = center (is-contr-map-is-equiv (is-equiv-postcomp-f Y) id)
  in
  is-equiv-has-inverse
    ( pr1 sec-f)
    ( htpy-eq (pr2 sec-f))
    ( htpy-eq
      ( ap ( pr1)
           ( eq-is-contr'
             ( is-contr-map-is-equiv (is-equiv-postcomp-f X) f)
             ( pair ((pr1 sec-f) âˆ˜ f) (ap (Î» t â†’ t âˆ˜ f) (pr2 sec-f)))
             ( pair id refl))))

is-equiv-postcomp-is-equiv :
  {l1 l2 : Level} {X : UU l1} {Y : UU l2} (f : X â†’ Y) â†’ is-equiv f â†’
  ({l3 : Level} (A : UU l3) â†’ is-equiv (postcomp A f))
is-equiv-postcomp-is-equiv {X = X} {Y = Y} f is-equiv-f A =
  is-equiv-has-inverse 
    ( postcomp A (map-inv-is-equiv is-equiv-f))
    ( Î» g â†’ eq-htpy (htpy-right-whisk (issec-map-inv-is-equiv is-equiv-f) g))
    ( Î» h â†’ eq-htpy (htpy-right-whisk (isretr-map-inv-is-equiv is-equiv-f) h))

equiv-postcomp :
  {l1 l2 l3 : Level} {X : UU l1} {Y : UU l2} (A : UU l3) â†’
  (X â‰ƒ Y) â†’ (A â†’ X) â‰ƒ (A â†’ Y)
pr1 (equiv-postcomp A e) = postcomp A (map-equiv e)
pr2 (equiv-postcomp A e) =
  is-equiv-postcomp-is-equiv (map-equiv e) (is-equiv-map-equiv e) A

is-emb-postcomp-is-emb :
  {l1 l2 l3 : Level} (A : UU l3) {X : UU l1} {Y : UU l2} (f : X â†’ Y) â†’
  is-emb f â†’ is-emb (postcomp A f)
is-emb-postcomp-is-emb A f is-emb-f =
  is-emb-is-prop-map
    ( is-trunc-map-postcomp-is-trunc-map neg-one-ğ•‹ A f
      ( is-prop-map-is-emb is-emb-f))

is-emb-is-emb-postcomp :
  {l1 l2 : Level} {X : UU l1} {Y : UU l2} (f : X â†’ Y) â†’
  ({l : Level} (A : UU l) â†’ is-emb (postcomp A f)) â†’ is-emb f
is-emb-is-emb-postcomp f is-emb-post-f =
  is-emb-is-prop-map
    ( is-trunc-map-is-trunc-map-postcomp neg-one-ğ•‹ f
      ( Î» A â†’ is-prop-map-is-emb (is-emb-post-f A)))

is-prop-leq-â„• :
  (m n : â„•) â†’ is-prop (leq-â„• m n)
is-prop-leq-â„• zero-â„• zero-â„• = is-prop-unit
is-prop-leq-â„• zero-â„• (succ-â„• n) = is-prop-unit
is-prop-leq-â„• (succ-â„• m) zero-â„• = is-prop-empty
is-prop-leq-â„• (succ-â„• m) (succ-â„• n) = is-prop-leq-â„• m n

leq-â„•-Prop : â„• â†’ â„• â†’ UU-Prop lzero
pr1 (leq-â„•-Prop m n) = leq-â„• m n
pr2 (leq-â„•-Prop m n) = is-prop-leq-â„• m n
